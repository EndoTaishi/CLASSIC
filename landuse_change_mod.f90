MODULE LANDUSE_CHANGE

! CENTRAL MODULE FOR ALL LAND USE CHANGE OPERATIONS

! J. MELTON. JAN 11, 2013

IMPLICIT NONE

REAL, PARAMETER :: SEED = 0.001 ! SEED PFT FRACTION, SAME AS IN COMPETITION
                                ! IN MOSAIC MODE, ALL TILES ARE GIVEN THIS
                                ! AS A MINIMUM

! SUBROUTINES CONTAINED IN THIS MODULE:
PUBLIC  :: INITIALIZE_LUC
PUBLIC  :: READIN_LUC
PUBLIC  :: LUC
PRIVATE :: ADJUST_LUC_FRACS

CONTAINS

!-------------------------------------------------------------------------------------------------------------
SUBROUTINE INITIALIZE_LUC(NLAT,NMOS,IYEAR,LUCDAT,NMTEST,NLTEST,&
                          MOSAIC,ICC,ICAN,ICP1,NOL2PFTS,CYCLEMET,   &
                          CYLUCYR,LUCYR,FCANROW,FAREROW,NFCANCMXROW,  &
                          PFCANCMXROW,FCANCMXROW,REACH_EOF)

!           CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
!                    LUC INITIAL READ-IN SUBROUTINE 
!
!     9  JAN. 2013  - THIS SUBROUTINE TAKES IN LUC INFORMATION FROM
!     J. MELTON       A LUC FILE AND ADAPTS THEM FOR RUNCLASSCTEM
!                     IT IS RUN ONCE TO SET UP THE LUC INFO BEFORE THE 
!                     MODEL TIMESTEPPING BEGINS.
!		      

IMPLICIT NONE

! ARGUMENTS:

! INPUTS
INTEGER, INTENT(IN) :: NMOS
INTEGER, INTENT(IN) :: NLAT
INTEGER, INTENT(IN) :: IYEAR
CHARACTER(80), INTENT(IN) :: LUCDAT
LOGICAL, INTENT(IN) :: MOSAIC
INTEGER, INTENT(IN) :: NMTEST
INTEGER, INTENT(IN) :: NLTEST
INTEGER, INTENT(IN) :: ICC
INTEGER, INTENT(IN) :: ICAN
INTEGER, DIMENSION(ICAN), INTENT(IN) :: NOL2PFTS
INTEGER, INTENT(IN) :: ICP1
LOGICAL, INTENT(IN) :: CYCLEMET
INTEGER, INTENT(IN) :: CYLUCYR 


! UPDATES
REAL, DIMENSION(NLAT,NMOS,ICP1), INTENT(INOUT) :: FCANROW
REAL, DIMENSION(NLAT,NMOS),      INTENT(INOUT) :: FAREROW
LOGICAL, INTENT(INOUT) :: REACH_EOF

! OUTPUTS
REAL, DIMENSION(NLAT,NMOS,ICC), INTENT(OUT) :: PFCANCMXROW
REAL, DIMENSION(NLAT,NMOS,ICC), INTENT(OUT) :: FCANCMXROW
REAL, DIMENSION(NLAT,NMOS,ICC), INTENT(OUT) :: NFCANCMXROW
INTEGER, INTENT(OUT) :: LUCYR

! LOCAL VARIABLES:
REAL, DIMENSION(ICC) :: TEMPARRAY
REAL :: TEMP
INTEGER :: J,M,I,N
INTEGER :: K2,K1,STRLEN

!-------------------------

! IT IS THE FIRST YEAR, SO PREPARE THE LUC DATA:

        ! OPEN THE LUC FILE

        OPEN(UNIT=90,FILE=LUCDAT(1:STRLEN(LUCDAT))//'.LUC')

        READ (90,*) 
        READ (90,*)
        READ (90,*)

!       GET FIRST YEAR OF LUC DATA
!       NOTE WE LOAD THE NFCANCMX, NOT PFCANCMX ARRAY. THIS IS BECAUSE THIS
!       NFCANCMX VALUE IS PASSED TO THE PFCANCMX ARRAY AT THE START OF EACH SIMULATION
!       YEAR

        DO I = 1, NLTEST
         IF (.NOT. MOSAIC) THEN  !COMPOSITE
           READ (90,*) LUCYR,(NFCANCMXROW(I,1,J),J=1,ICC)
         ELSE                    !MOSAIC
           READ (90,*) LUCYR,(TEMPARRAY(J),J=1,ICC)
           DO M = 1, NMTEST-1 !AS NMTEST-1 = ICC
             J = M
             NFCANCMXROW(I,M,J) = TEMPARRAY(M)
           ENDDO !M LOOP
         ENDIF
        ENDDO !NLTEST

!       NEXT UPDATE OUR LUC DATA IF EITHER OF THE FOLLOWING CONDITIONS ARE MET:
!       1) WE ARE CYCLING THE MET DATA AND THE LUC YEAR WE JUST READ IN IS LESS
!       THAN THE YEAR WE WANT TO CYCLE OVER (ASSUMING IT IS NOT DEFAULTED TO 
!       -9999) OR,
!       2) WE ARE NOT CYCLING OVER THE MET DATA SO WE JUST WANT TO GET THE SAME
!       YEAR OF LUC DATA AS THE MET DATA WE READ IN ABOVE IN PREPARATION FOR OUR
!       TRANSIENT RUN.

        DO WHILE ((CYCLEMET .AND. LUCYR .LT. CYLUCYR             &
          .AND. CYLUCYR .NE. -9999) .OR. (.NOT. CYCLEMET .AND.  &
          LUCYR .LT. IYEAR))

!           GET THE LUC DATA
            DO I = 1, NLTEST
             IF (.NOT. MOSAIC) THEN  !COMPOSITE
               READ (90,*,END=999) LUCYR,(NFCANCMXROW(I,1,J),J=1,ICC)
             ELSE                    !MOSAIC
               READ (90,*,END=999) LUCYR,(TEMPARRAY(J),J=1,ICC)
               DO M = 1, NMTEST-1 !NMTEST-1 SAME AS ICC
                J = M
                NFCANCMXROW(I,M,J) = TEMPARRAY(M) 
               ENDDO !M LOOP
             ENDIF
            ENDDO !NLTEST
        ENDDO  !WHILE LOOP

!       GET FCANS FOR USE BY CLASS USING THE NFCANCMXS JUST READ IN
        K1=0
        DO 997 J = 1, ICAN
          IF(J.EQ.1) THEN
            K1 = K1 + 1
          ELSE
            K1 = K1 + NOL2PFTS(J-1)
          ENDIF
          K2 = K1 + NOL2PFTS(J) - 1
          DO 998 N = K1, K2
            DO I = 1, NLTEST
             DO M = 1, NMTEST
              IF (.NOT. MOSAIC) THEN !COMPOSITE

               FCANROW(I,M,J)=FCANROW(I,M,J)+NFCANCMXROW(I,M,N) 

              ELSE IF (MOSAIC .AND. NFCANCMXROW(I,M,N) .GT. SEED) THEN
!              THIS TILE HAS SOME PLANTS SO OVERWRITE THE SEED FRACTION WITH
!              AN ACTUAL FRACTION

!              NOTE: THE SEED FRACTION HAS ALREADY BEEN ASSIGNED IN RUNCLASSCTEM
!              PRIOR TO ENTERING THIS SUBROUTINE.
               FAREROW(I,M)=NFCANCMXROW(I,M,N)    

              ENDIF
             ENDDO
            ENDDO
998       CONTINUE
997     CONTINUE

!       (RE)FIND THE BARE FRACTION FOR FAREROW(I,ICC+1)
         IF (MOSAIC) THEN
          DO I = 1, NLTEST
          TEMP = 0.
           DO M = 1, NMTEST-1
            TEMP = TEMP + FAREROW(I,M)
           ENDDO

           FAREROW(I,NMTEST) = 1.0 - TEMP

          ENDDO
         ENDIF

          ! CHECK THAT THE BARE FRACTION IS POSSIBLE (>0) AND IF NOT THEN
          ! REDUCE THE OTHER PFTS PROPORTIONALLY TO MAKE A NON-NEGATIVE BARE
          ! GROUND FRACTION.

          DO I = 1, NLTEST
           TEMP = 0.0
           IF (MOSAIC) THEN
            IF (FAREROW(I,NMTEST) < SEED) THEN

             NFCANCMXROW= ADJUST_LUC_FRACS(NLAT,NMOS,I,NMTEST,NLTEST,ICC, &
                                          NFCANCMXROW,FAREROW(I,NMTEST))

             DO M = 1, NMTEST
                N = M
                FAREROW(I,M)=NFCANCMXROW(I,M,N)  
                TEMP = TEMP + FAREROW(I,M)  
             ENDDO

             FAREROW(I,NMTEST) = 1.0 - TEMP

            ENDIF !FAREROW<SEED
           ENDIF !MOSAIC
          ENDDO !NLTEST


!       ASSIGN THE PRESENT PFT FRACTIONS FROM THOSE JUST READ IN
        DO J = 1, ICC
          DO I = 1, NLTEST
           DO M = 1, NMTEST
            IF (.NOT. MOSAIC) THEN  !COMPOSITE 
             FCANCMXROW(I,M,J)=NFCANCMXROW(I,M,J)
            ENDIF !MOSAIC
!            ENSURE THAT THE FRACTION IS >= SEED
             PFCANCMXROW(I,M,J)=MAX(SEED,NFCANCMXROW(I,M,J))
           ENDDO
          ENDDO
        ENDDO

!       BACK UP ONE YEAR IN THE LUC FILE 

        DO I = 1, NLTEST
           BACKSPACE(90)  
        ENDDO

RETURN

999    CONTINUE
  
! END OF THE LUC FILE IS REACHED. CLOSE AND TELL MAIN PROGRAM TO EXIT
        CLOSE(90)
        REACH_EOF = .TRUE.

END SUBROUTINE INITIALIZE_LUC

!=======================================================================

SUBROUTINE READIN_LUC(NLAT,NMOS,IYEAR,NMTEST,NLTEST,MOSAIC,ICC,LUCYR, &
                      NFCANCMXROW,REACH_EOF)

!           CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
!                    LUC ANNUAL READ-IN SUBROUTINE 
!
!     9  JAN. 2013  - THIS SUBROUTINE TAKES IN LUC INFORMATION FROM
!     J. MELTON       A LUC FILE ANNUALLY AND ADAPTS THEM FOR RUNCLASSCTEM
!		      

IMPLICIT NONE

! ARGUMENTS

! INPUTS
INTEGER, INTENT(IN) :: NMOS
INTEGER, INTENT(IN) :: NLAT
INTEGER, INTENT(IN) :: IYEAR
LOGICAL, INTENT(IN) :: MOSAIC
INTEGER, INTENT(IN) :: NMTEST
INTEGER, INTENT(IN) :: NLTEST
INTEGER, INTENT(IN) :: ICC

! UPDATES
INTEGER, INTENT(INOUT) :: LUCYR
LOGICAL, INTENT(INOUT) :: REACH_EOF

! OUTPUTS
REAL, DIMENSION(NLAT,NMOS,ICC), INTENT(OUT) :: NFCANCMXROW

! LOCAL VARIABLES
REAL, DIMENSION(ICC) :: TEMPARRAY
REAL :: TEMP
INTEGER :: J,M,I
INTEGER :: K1,K2,N
REAL :: BARE_GROUND_FRAC

!-------------------------

!IT IS SUBSEQUENT YEARS SO READ IN AND ADJUST THE LUC FILE INFO.

         DO WHILE (LUCYR <= IYEAR) 
           DO I = 1, NLTEST
            IF (.NOT. MOSAIC) THEN  !COMPOSITE
              READ (90,*,END=999) LUCYR,(NFCANCMXROW(I,1,J),J=1,ICC)
            ELSE                    !MOSAIC
              READ (90,*,END=999) LUCYR,(TEMPARRAY(J),J=1,ICC)
              DO M = 1, NMTEST-1    !NMTEST-1 SAME AS ICC
               J = M
               NFCANCMXROW(I,M,J) = MAX(SEED,TEMPARRAY(M)) 
              ENDDO !M LOOP
            ENDIF
           ENDDO !NLTEST
         ENDDO !LUCYR<IYEAR

!       (RE)FIND THE BARE FRACTION FOR FAREROW(I,ICC+1)
         IF (MOSAIC) THEN
          DO I = 1, NLTEST
          TEMP = 0.0
           DO M = 1, NMTEST-1
            J = M
            TEMP = TEMP + NFCANCMXROW(I,M,J)
           ENDDO

            BARE_GROUND_FRAC = 1.0- TEMP

          ENDDO

          DO I = 1, NLTEST
           IF (BARE_GROUND_FRAC < SEED) THEN

             NFCANCMXROW= ADJUST_LUC_FRACS(NLAT,NMOS,I,NMTEST,NLTEST,ICC, &
                                           NFCANCMXROW,BARE_GROUND_FRAC)

           ENDIF !BARE_GROUND_FRAC<SEED
          ENDDO !NLTEST
         ENDIF !MOSAIC

RETURN

999 CONTINUE

! END OF THE LUC FILE IS REACHED. CLOSE AND TELL MAIN PROGRAM TO EXIT
        CLOSE(90)
        REACH_EOF = .TRUE.

END SUBROUTINE READIN_LUC

!=======================================================================

SUBROUTINE    LUC(     ICC,      ILG,      IL1,       IL2,  & 
                              IC, NOL2PFTS,    L2MAX,               & !1    
                        GRCLAREA, PFCANCMX, NFCANCMX,      IDAY,    & !2    
                         TODFRAC,  YESFRAC, INTERPOL,               & !3    
!    ----------------------- INPUTS ABOVE THIS LINE -------------       
                         GLEAFMAS, BLEAFMAS, STEMMASS, ROOTMASS,    & !4  
                         LITRMASS, SOILCMAS, VGBIOMAS, GAVGLTMS,    & !5   
                         GAVGSCMS,  FCANCMX,   FCANMX,              & !6
!    ----------- UPDATES ABOVE THIS LINE, OUTPUTS BELOW ---------
                         LUCEMCOM, LUCLTRIN, LUCSOCIN)                !7  
!
!     ----------------------------------------------------------------
!
!           CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
!                       LAND USE CHANGE SUBROUTINE 
!
!     02  JAN. 2004 - THIS SUBROUTINE DEALS WITH THE CHANGES IN THE LAND
!     V. ARORA        COVER AND ESTIMATES LAND USE CHANGE (LUC)
!                     RELATED CARBON EMISSIONS. BASED ON WHETHER THERE
!                     IS CONVERSION OF FORESTS/GRASSLAND TO CROP AREA,
!                     OR CROPLANDS ABANDONMENT, THIS SUBROUTINE
!                     REALLOCATES CARBON FROM LIVE VEGETATION TO LITTER
!                     AND SOIL C COMPONENTS. THE DECOMPOSITION FROM THE
!                     LITTER AND SOIL C POOLS THUS IMPLICITLY MODELS LUC
!                     RELATED CARBON EMISSIONS. SET OF RULES ARE
!                     FOLLOWED TO DETERMINE THE FATE OF CARBON THAT
!                     RESULTS FROM DEFORESTATION OR REPLACEMENT OF
!                     GRASSLANDS BY CROPS. 
!
!     ----------------------------------------------------------------
!     INPUTS
!
!     ICC       - NO OF PFTS FOR USE BY CTEM, CURRENTLY 9
!     IC        - NO OF PFTS FOR USE BY CLASS, CURRENTLY 4
!     ILG      - NO. OF GRID CELLS IN LATITUDE CIRCLE
!     IL1, IL2  - IL1=1, IL2=ILG
!     NOL2PFTS  - NUMBER OF LEVEL 2 PFTS
!     L2MAX     - MAXIMUM NUMBER OF LEVEL 2 PFTS
!     FCANCMX   - MAX. FRACTIONAL COVERAGES OF CTEM'S 9 PFTS. 
!     GRCLAREA  - GCM GRID CELL AREA, KM2
!     PFCANCMX  - PREVIOUS MAX. FRACTIONAL COVERAGES OF CTEM'S 9 PFTS. 
!     NFCANCMX  - NEXT MAX. FRACTIONAL COVERAGES OF CTEM'S 9 PFTS. 
!     IDAY      - DAY OF YEAR
!     TODFRAC   - TODAY'S FRACTIONAL COVERAGE OF ALL PFTS 
!     YESFRAC   - YESTERDAY'S FRACTIONAL COVERAGE OF ALL PFTS 
!     INTERPOL  - IF TODFRAC & YESFRAC ARE PROVIDED THEN INTERPOL MUST
!                 BE SET TO FALSE SO THAT THIS SUBROUTINE DOESN'T DO ITS
!                 OWN INTERPOLATION USING PFCANCMX AND NFCANCMX WHICH
!                 ARE YEAR END VALUES     
!     UPDATES

!     GLEAFMAS  - GREEN OR LIVE LEAF MASS IN KG C/M2, FOR THE 9 PFTS
!     BLEAFMAS  - BROWN OR DEAD LEAF MASS IN KG C/M2, FOR THE 9 PFTS
!     STEMMASS  - STEM BIOMASS IN KG C/M2, FOR THE 9 PFTS
!     ROOTMASS  - ROOT BIOMASS IN KG C/M2, FOR THE 9 PFTS
!     LITRMASS  - LITTER MASS IN KG C/M2, FOR THE 9 PFTS + BARE
!     SOILCMAS  - SOIL C MASS IN KG C/M2, FOR THE 9 PFTS + BARE
!     VGBIOMAS  - GRID AVERAGED VEGETATION BIOMASS, KG C/M2
!     GAVGLTMS  - GRID AVERAGED LITTER MASS, KG C/M2
!     GAVGSCMS  - GRID AVERAGED SOIL C MASS, KG C/M2

!     OUTPUTS

!     FCANMX   - FRACTIONAL COVERAGES OF CLASS 4 PFTS (THESE ARE FOUND BASED 
!                ON NEW FCANCMXS)
!     LUCEMCOM - LUC RELATED CARBON EMISSION LOSSES FROM COMBUSTION
!                U-MOL CO2/M2.SEC
!     LUCLTRIN - LUC RELATED INPUT TO LITTER POOL, U-MOL CO2/M2.SEC
!     LUCSOCIN - LUC RELATED INPUT TO SOIL CARBON POOL, U-MOL CO2/M2.SEC

!     ----------------------------------------------------------------    

      IMPLICIT NONE

      INTEGER ICC,  ILG, IL1, IL2, IC, I, J, K, M, N, KK, K1, K2, IDAY
      INTEGER    NOL2PFTS(IC),             L2MAX,       LUCTKPLC(ILG)
      INTEGER FRACIORD(ILG,ICC), TREATIND(ILG,ICC),       BAREIORD(ILG)

      LOGICAL  INTERPOL 
!      
      REAL  GLEAFMAS(ILG,ICC), BLEAFMAS(ILG,ICC),  STEMMASS(ILG,ICC), & !1
            ROOTMASS(ILG,ICC),  FCANCMX(ILG,ICC),  PFCANCMX(ILG,ICC), & !2
                VGBIOMAS(ILG),             ZERO,  SOILCMAS(ILG,ICC+1),& !3 
          LITRMASS(ILG,ICC+1),     GAVGLTMS(ILG),      GAVGSCMS(ILG), & !4
            NFCANCMX(ILG,ICC),  FCANCMY(ILG,ICC),   TODFRAC(ILG,ICC), & !5
             YESFRAC(ILG,ICC)

      REAL    FCANMX(ILG,IC),  DELFRAC(ILG,ICC),          COMBUST(3), & !1
                    PAPER(3),      FURNITURE(3),   ABVGMASS(ILG,ICC), & !2
                 BMASTHRS(2),    GRCLAREA(ILG),   COMBUSTC(ILG,ICC),  & !3
             PAPERC(ILG,ICC), FURNTURC(ILG,ICC),  INCRLITR(ILG,ICC),  & !4
           INCRSOLC(ILG,ICC),          TOLRNCE1,            TOLRNCE2, & !5
               CHOPEDBM(ILG),           KM2TOM2

      REAL         REDUBMAS1,              TERM,       BAREFRAC(ILG), & 
                GRSUMCOM(ILG),     GRSUMPAP(ILG),      GRSUMFUR(ILG), & !1
                GRSUMLIT(ILG),     GRSUMSOC(ILG),      PBAREFRA(ILG), & !2
                GRDENCOM(ILG),     GRDENPAP(ILG),      GRDENFUR(ILG), & !3
                GRDENLIT(ILG),     GRDENSOC(ILG),      TOTCMASS(ILG), & !4
                TOTLMASS(ILG),     TOTDMAS1(ILG),      NTOTCMAS(ILG), & !5
                NTOTLMAS(ILG),     NTOTDMS1(ILG),      LUCEMCOM(ILG), & !6
                PVGBIOMS(ILG),     PGAVLTMS(ILG),      PGAVSCMS(ILG), & !7
                    REDUBMAS2,     LUCLTRIN(ILG),       LUCSOCIN(ILG),& !7
                TOTDMAS2(ILG),     NTOTDMS2(ILG)


!     HOW MUCH DEFORESTED/CHOPPED OFF BIOMASS IS COMBUSTED
      DATA COMBUST/0.15, 0.30, 0.45/

!     HOW MUCH DEFORESTED/CHOPPED OFF BIOMASS GOES INTO SHORT TERM
!     STORAGE SUCH AS PAPER
      DATA PAPER/0.70, 0.70, 0.55/

!     HOW MUCH DEFORESTED/CHOPPED OFF BIOMASS GOES INTO LONG TERM
!     STORAGE SUCH AS FURNITURE
      DATA FURNITURE/0.15, 0.0, 0.0/

!     BIOMASS THRESHOLDS FOR DETERMINING IF DEFORESTED AREA IS A FOREST,
!     A SHRUBLAND, OR A BUSH KG C/M2
      DATA BMASTHRS/4.0, 1.0/

      DATA ZERO/1.0E-20/

      DATA TOLRNCE1/0.50/  ! KG C, TOLERANCE OF TOTAL C BALANCE 
      DATA TOLRNCE2/0.005/ ! KG C/M2, TOLERANCE FOR TOTAL C DENSITY

      DATA KM2TOM2/1.0E+06/ ! CHANGES FROM KM2 TO M2
!     ---------------------------------------------------------------

      IF(ICC.NE.9)                               CALL XIT('LUC',-1)  
      IF(IC.NE.4)                                CALL XIT('LUC',-2)  

!     ------------------------------------------------------------------

!     CHECK IF FRACTIONS OF DEFORESTED BIOMASS THAT IS COMBUSTED, AND
!     TURNED INTO PAPER AND FURNITIRE ADD TO 1.0

      DO 100 K = 1, 3
        IF( ABS(COMBUST(K)+PAPER(K)+FURNITURE(K)-1.0).GT.ZERO )THEN
           WRITE(6,*)'COMBUST + PAPER + FURNITURE DO NOT ADD TO 1.0'
           WRITE(6,*)'COMBUST(',K,')=',COMBUST(K)
           WRITE(6,*)'PAPER(',K,')=',PAPER(K)
           WRITE(6,*)'FURNITURE(',K,')=',FURNITURE(K)
           CALL XIT('LUC',-3)
        ENDIF
100   CONTINUE

!     FIND/USE PROVIDED CURRENT AND PREVIOUS DAY'S FRACTIONAL COVERAGE 

      IF(INTERPOL) THEN ! PERFORM INTERPOLATION 
       DO 110 J = 1, ICC
        DO 111 I = IL1, IL2

          DELFRAC(I,J)=NFCANCMX(I,J)-PFCANCMX(I,J) !CHANGE IN FRACTION
          DELFRAC(I,J)=DELFRAC(I,J)/365.0
          FCANCMX(I,J)=PFCANCMX(I,J)+(REAL(IDAY)*DELFRAC(I,J)) !  CURRENT DAY
          FCANCMY(I,J)=PFCANCMX(I,J)+(REAL(IDAY-1)*DELFRAC(I,J)) ! PREVIOUS DAY

          IF( FCANCMX(I,J).LT.0.0.AND.ABS(FCANCMX(I,J)).LT.1.0E-05)THEN
            FCANCMX(I,J)=0.0
          ELSE IF( FCANCMX(I,J).LT.0.0.AND. &
                ABS(FCANCMX(I,J)).GE.1.0E-05)THEN
            WRITE(6,*)'FCANCMX(',I,',',J,')=',FCANCMX(I,J)
            WRITE(6,*)'FRACTIONAL COVERAGE CANNOT BE NEGATIVE'
            CALL XIT('LUC',-4)
          ENDIF

          IF(FCANCMY(I,J).LT.0.0.AND.ABS(FCANCMY(I,J)).LT.1.0E-05)THEN    
            FCANCMY(I,J)=0.0
          ELSE IF( FCANCMY(I,J).LT.0.0.AND. &
         ABS(FCANCMY(I,J)).GE.1.0E-05)THEN
            WRITE(6,*)'FCANCMY(',I,',',J,')=',FCANCMY(I,J)
            WRITE(6,*)'FRACTIONAL COVERAGE CANNOT BE NEGATIVE'
            CALL XIT('LUC',-5)
          ENDIF

111     CONTINUE
110    CONTINUE
      ELSE IF(.NOT. INTERPOL) THEN ! USE PROVIDED VALUES BUT STILL CHECK
!                                   THEY ARE NOT -VE
       DO 115 J = 1, ICC
        DO 116 I = IL1, IL2
          FCANCMX(I,J) = TODFRAC(I,J)   
          FCANCMY(I,J) = YESFRAC(I,J)   

          IF( FCANCMX(I,J).LT.0.0.AND.ABS(FCANCMX(I,J)).LT.1.0E-05)THEN
            FCANCMX(I,J)=0.0
          ELSE IF( FCANCMX(I,J).LT.0.0.AND. &
         ABS(FCANCMX(I,J)).GE.1.0E-05)THEN
            WRITE(6,*)'FCANCMX(',I,',',J,')=',FCANCMX(I,J)
            WRITE(6,*)'FRACTIONAL COVERAGE CANNOT BE NEGATIVE'
            CALL XIT('LUC',-4)
          ENDIF

          IF(FCANCMY(I,J).LT.0.0.AND.ABS(FCANCMY(I,J)).LT.1.0E-05)THEN
            FCANCMY(I,J)=0.0
          ELSE IF( FCANCMY(I,J).LT.0.0.AND. &
         ABS(FCANCMY(I,J)).GE.1.0E-05)THEN
            WRITE(6,*)'FCANCMY(',I,',',J,')=',FCANCMY(I,J)
            WRITE(6,*)'FRACTIONAL COVERAGE CANNOT BE NEGATIVE'
            CALL XIT('LUC',-5)
          ENDIF

116     CONTINUE
115    CONTINUE
      ELSE 
       WRITE(6,*)'INTERPOL MUST BE 0 OR 1'
       WRITE(6,*)'INTERPOL  = ',INTERPOL
       CALL XIT('LUC',-6)
      ENDIF

!     CHECK IF THIS YEAR'S FRACTIONAL COVERAGES HAVE CHANGED OR NOT
!     FOR ANY PFT

      DO 150 I = IL1, IL2
        LUCTKPLC(I)=0  ! DID LAND USE CHANGE TAKE PLACE FOR ANY PFT
150   CONTINUE         ! IN THIS GRID CELL

      DO 200 J = 1, ICC
        DO 250 I = IL1, IL2
          IF ( (ABS(FCANCMX(I,J)-FCANCMY(I,J))).GT.ZERO ) THEN
            LUCTKPLC(I)=1 ! YES, LUC DID TAKE PLACE IN THIS GRID CELL
          ENDIF
250     CONTINUE
200   CONTINUE

!     -------------------------------------------------------------------
 
!     INITIALIZATION

      DO 260 J = 1, IC
        DO 261 I = IL1, IL2
          IF(LUCTKPLC(I).EQ.1)THEN
            FCANMX(I,J)=0.0 ! FRACTIONAL COVERAGE OF CLASS' PFTS
          ENDIF
261     CONTINUE      
260   CONTINUE      

      DO 270 J = 1, ICC
        DO 271 I = IL1, IL2
          FRACIORD(I,J)=0   !FRACTIONAL COVERAGE INCREASE OR DECREASE
!                           !INCREASE +1, DECREASE -1
          ABVGMASS(I,J)=0.0 !ABOVE-GROUND BIOMASS
          TREATIND(I,J)=0   !TREATMENT INDEX FOR COMBUST, PAPER, & FURNITURE
          COMBUSTC(I,J)=0.0 !TOTAL CARBON FROM DEFORESTATION- COMBUSTION
          PAPERC(I,J)=0.0   !TOTAL CARBON FROM DEFORESTATION- PAPER
          FURNTURC(I,J)=0.0 !TOTAL CARBON FROM DEFORESTATION- FURNITURE

271     CONTINUE  
270   CONTINUE      

      DO 280 I = IL1, IL2
        PVGBIOMS(I)=VGBIOMAS(I)  ! STORE GRID AVERAGE QUANTITIES IN
        PGAVLTMS(I)=GAVGLTMS(I)  ! TEMPORARY ARRAYS
        PGAVSCMS(I)=GAVGSCMS(I)

        VGBIOMAS(I)=0.0
        GAVGLTMS(I)=0.0
        GAVGSCMS(I)=0.0

        BAREFRAC(I)=1.0          ! INITIALIZE BARE FRACTION TO 1.0
        PBAREFRA(I)=1.0          ! INITIALIZE PREVIOUS YEARS'S BARE FRACTION TO 1.0

        GRSUMCOM(I)=0.0          ! GRID SUM OF COMBUSTION CARBON FOR ALL
                                 ! PFTS THAT ARE CHOPPED
        GRSUMPAP(I)=0.0          ! SIMILARLY FOR PAPER, 
        GRSUMFUR(I)=0.0          ! FURNITURE,
        GRSUMLIT(I)=0.0          ! LITTER, AND
        GRSUMSOC(I)=0.0          ! SOIL C

        GRDENCOM(I)=0.0          ! GRID AVERAGED DENSITIES FOR COMBUSTION CARBON,
        GRDENPAP(I)=0.0          ! PAPER,
        GRDENFUR(I)=0.0          ! FURNITURE, 
        GRDENLIT(I)=0.0          ! LITTER, AND
        GRDENSOC(I)=0.0          ! SOIL C

        TOTCMASS(I)=0.0          ! TOTAL C MASS (LIVE+DEAD)
        TOTLMASS(I)=0.0          ! TOTAL C MASS (LIVE)
        TOTDMAS1(I)=0.0          ! TOTAL C MASS (DEAD) LITTER
        TOTDMAS2(I)=0.0          ! TOTAL C MASS (DEAD) SOIL C

!       AND THE SAME 3 QUANTITIES AS ABOVE AFTER LUC TREATMENT

        NTOTCMAS(I)=0.0          ! TOTAL C MASS (LIVE+DEAD)
        NTOTLMAS(I)=0.0          ! TOTAL C MASS (LIVE)
        NTOTDMS1(I)=0.0          ! TOTAL C MASS (DEAD) LITTER
        NTOTDMS2(I)=0.0          ! TOTAL C MASS (DEAD) SOIL C

        LUCEMCOM(I)=0.0          ! LUC RELATED COMBUSTION EMISSION LOSSES
        LUCLTRIN(I)=0.0          ! LUC RELATED INPUTS TO LITTER POOL
        LUCSOCIN(I)=0.0          ! LUC RELATED INPUTS TO SOIL C POOL

        BAREIORD(I)=0            ! BARE FRACTION INCREASES OR DECREASES
        CHOPEDBM(I)=0.0          ! CHOPPED OFF BIOMASS
280   CONTINUE

!     INITIALIZATION ENDS

!     -------------------------------------------------------------------

!     IF LAND USE CHANGE HAS TAKEN PLACE THEN GET FCANMXS FOR USE BY 
!     CLASS BASED ON THE NEW FCANCMXS

      K1=0
      DO 300 J = 1, IC
        IF(J.EQ.1) THEN
          K1 = K1 + 1
        ELSE
          K1 = K1 + NOL2PFTS(J-1)
        ENDIF
        K2 = K1 + NOL2PFTS(J) - 1
        DO 301 M = K1, K2
          DO 302 I = IL1, IL2
            IF(LUCTKPLC(I).EQ.1)THEN
              FCANMX(I,J)=FCANMX(I,J)+FCANCMX(I,M)
            ENDIF
            BAREFRAC(I)=BAREFRAC(I)-FCANCMX(I,M)
302       CONTINUE
301     CONTINUE
300   CONTINUE

!     FIND PREVIOUS DAY'S BARE FRACTION USING PREVIOUS DAY'S FCANCMXS

      DO 310 J = 1, ICC
        DO 311 I = IL1, IL2
          PBAREFRA(I)=PBAREFRA(I)-FCANCMY(I,J)
311     CONTINUE
310   CONTINUE

!     BASED ON SIZES OF 3 LIVE POOLS AND 2 DEAD POOLS WE ESTIMATE THE
!     TOTAL AMOUNT OF C IN EACH GRID CELL.

      DO 320 J = 1, ICC
        DO 321 I = IL1, IL2
          TOTLMASS(I)=TOTLMASS(I)+ &
                     (FCANCMY(I,J)*(GLEAFMAS(I,J)+BLEAFMAS(I,J)+ &
                      STEMMASS(I,J)+ROOTMASS(I,J))*GRCLAREA(I) &
                      *KM2TOM2)
321     CONTINUE
320   CONTINUE

      DO 340 J = 1, ICC+1
        DO 341 I = IL1, IL2
          IF(J.LT.ICC+1) THEN
            TERM = FCANCMY(I,J)
          ELSE IF(J.EQ.ICC+1) THEN
            TERM = PBAREFRA(I)
          ENDIF
          TOTDMAS1(I)=TOTDMAS1(I)+ &
                     (TERM*LITRMASS(I,J)*GRCLAREA(I)*KM2TOM2)
          TOTDMAS2(I)=TOTDMAS2(I)+ & 
                     (TERM*SOILCMAS(I,J)*GRCLAREA(I)*KM2TOM2)

341     CONTINUE
340   CONTINUE

      DO 350 I = IL1, IL2
        TOTCMASS(I)=TOTLMASS(I)+TOTDMAS1(I)+TOTDMAS2(I)
350   CONTINUE

!     BARE FRACTIONS CANNOT BE NEGATIVE

      DO 440 I = IL1, IL2
        IF( PBAREFRA(I).LT.0.0.AND.ABS(PBAREFRA(I)).LT.1.0E-05 )THEN
          PBAREFRA(I)=0.0
        ELSE IF(PBAREFRA(I).LT.0.0.AND.ABS(PBAREFRA(I)).GE.1.0E-05 )THEN
          WRITE(6,*)'BARE FRACTIONS CANNOT BE NEGATIVE'
          WRITE(6,*)'PREV. BARE FRACTION(',I,')  =',PBAREFRA(I)
          CALL XIT('LUC',-7)
        ENDIF

        IF( BAREFRAC(I).LT.0.0.AND.ABS(BAREFRAC(I)).LT.1.0E-05 )THEN
          BAREFRAC(I)=0.0
        ELSE IF(BAREFRAC(I).LT.0.0.AND.ABS(BAREFRAC(I)).GE.1.0E-05 )THEN
          WRITE(6,*)'BARE FRACTIONS CANNOT BE NEGATIVE'
          WRITE(6,*)'BARE FRACTION(',I,')  =',BAREFRAC(I)
          CALL XIT('LUC',-8)
        ENDIF
440   CONTINUE

!     FIND ABOVE GROUND BIOMASS AND TREATMENT INDEX FOR COMBUST, PAPER,
!     AND FURNITURE

      K1=0
      DO 500 J = 1, IC
        IF(J.EQ.1) THEN
          K1 = K1 + 1
        ELSE
          K1 = K1 + NOL2PFTS(J-1)
        ENDIF
        K2 = K1 + NOL2PFTS(J) - 1
        DO 510 M = K1, K2
          DO 520 I = IL1, IL2
            ABVGMASS(I,M)=GLEAFMAS(I,M)+BLEAFMAS(I,M)+STEMMASS(I,M)
            IF(J.EQ.1.OR.J.EQ.2) THEN  ! TREES
              IF(ABVGMASS(I,M).GE.BMASTHRS(1)) THEN !FOREST
                TREATIND(I,M)=1
              ELSE IF (ABVGMASS(I,M).LE.BMASTHRS(2)) THEN !BUSH 
                TREATIND(I,M)=3
              ELSE  !SHRUBLAND
                TREATIND(I,M)=2
              ENDIF
            ELSE                       !CROPS AND GRASSES
              TREATIND(I,M)=3
            ENDIF
520       CONTINUE
510     CONTINUE
500   CONTINUE

!     IF TREATMENT INDEX IS ZERO THEN SOMETHING IS WRONG - WE EXIT
!     JM EDIT -- ISN'T THIS IMPOSSIBLE, SO WE DON'T NEED TO CHECK?
      DO 530 J = 1, ICC
        DO 531 I = IL1, IL2
          IF(TREATIND(I,J).EQ.0)THEN
        WRITE(6,*)'TREATMENT INDEX ZERO FOR GRID CELL ',I,' AND PFT',J
            CALL XIT('LUC',-9)
          ENDIF
531     CONTINUE
530   CONTINUE

!     CHECK IF A PFT'S FRACTIONAL COVER IS INCREASING OR DECREASING

      DO 550 J = 1, ICC 
        DO 551 I = IL1, IL2
          IF( ( FCANCMX(I,J).GT.FCANCMY(I,J)) .AND. &
             (ABS(FCANCMY(I,J)-FCANCMX(I,J)).GT.ZERO) ) THEN
              FRACIORD(I,J)=1  ! INCREASING
          ELSE IF( ( FCANCMX(I,J).LT.FCANCMY(I,J)) .AND. &
                  (ABS(FCANCMY(I,J)-FCANCMX(I,J)).GT.ZERO) ) THEN
              FRACIORD(I,J)=-1 ! DECREASING
          ENDIF
551     CONTINUE
550   CONTINUE

!     CHECK IF BARE FRACTION INCREASES OF DECREASES

      DO 560 I = IL1, IL2
        IF( ( BAREFRAC(I).GT.PBAREFRA(I)) .AND. &
           (ABS(PBAREFRA(I)-BAREFRAC(I)).GT.ZERO) ) THEN
              BAREIORD(I)=1  !INCREASING
        ELSE IF ( ( BAREFRAC(I).LT.PBAREFRA(I)) .AND. &
                 (ABS(PBAREFRA(I)-BAREFRAC(I)).GT.ZERO) ) THEN
              BAREIORD(I)=-1 !DECREASING
        ENDIF
560   CONTINUE

      
!     IF THE FRACTIONAL COVERAGE OF PFTS INCREASES THEN SPREAD THEIR
!     LIVE & DEAD BIOMASS UNIFORMLY OVER THE NEW FRACTION. THIS 
!     EFFECTIVELY REDUCES THEIR PER M2 C DENSITY. 
 
      DO 570 J = 1, ICC
        DO 571 I = IL1, IL2
          IF(FRACIORD(I,J).EQ.1)THEN
            TERM = FCANCMY(I,J)/FCANCMX(I,J)
            GLEAFMAS(I,J)=GLEAFMAS(I,J)*TERM
            BLEAFMAS(I,J)=BLEAFMAS(I,J)*TERM
            STEMMASS(I,J)=STEMMASS(I,J)*TERM
            ROOTMASS(I,J)=ROOTMASS(I,J)*TERM
            LITRMASS(I,J)=LITRMASS(I,J)*TERM
            SOILCMAS(I,J)=SOILCMAS(I,J)*TERM
          ENDIF 
571     CONTINUE
570   CONTINUE

!     IF BARE FRACTION INCREASES THEN SPREAD ITS LITTER AND SOIL C
!     UNIFORMLY OVER THE INCREASED FRACTION

      DO 580 I = IL1, IL2
        IF(BAREIORD(I).EQ.1)THEN
          TERM = PBAREFRA(I)/BAREFRAC(I)
          LITRMASS(I,ICC+1)=LITRMASS(I,ICC+1)*TERM
          SOILCMAS(I,ICC+1)=SOILCMAS(I,ICC+1)*TERM
        ENDIF
580   CONTINUE

!     IF ANY OF THE PFTS FRACTIONAL COVERAGE DECREASES, THEN WE CHOP THE
!     ABOVEGROUND BIOMASS AND TREAT IT ACCORDING TO OUR RULES (BURN IT,
!     AND CONVERT IT INTO PAPER AND FURNITURE). THE BELOW GROUND LIVE
!     BIOMASS AND LITTER OF THIS PFTS GETS ASSIMILATED INTO LITTER OF
!     ALL PFTS (UNIFORMLY SPREAD OVER THE WHOLE GRID CELL), AND SOIL C
!     FROM THE CHOPPED OFF FRACTION OF THIS PFT, GETS ASSIMILATED INTO
!     SOIL C OF ALL EXISTING PFTS AS WELL.

      K1=0
      DO 600 J = 1, IC
        IF(J.EQ.1) THEN
          K1 = K1 + 1
        ELSE
          K1 = K1 + NOL2PFTS(J-1)
        ENDIF
        K2 = K1 + NOL2PFTS(J) - 1
        DO 610 M = K1, K2
          DO 620 I = IL1, IL2

            IF(FRACIORD(I,M).EQ.-1)THEN

!             CHOP OFF ABOVE GROUND BIOMASS 
              REDUBMAS1=(FCANCMY(I,M)-FCANCMX(I,M))*GRCLAREA(I) &
                       *ABVGMASS(I,M)*KM2TOM2

              IF(REDUBMAS1.LT.0.0)THEN
                WRITE(6,*)'REDUBMAS1 LESS THAN ZERO'
                WRITE(6,*)'FCANCMY-FCANCMX = ',  &
                          FCANCMY(I,M)-FCANCMX(I,M)
                WRITE(6,*)'GRID CELL = ',I,' PFT = ',M
                CALL XIT('LUC',-10)
              ENDIF

!             ROOTMASS NEEDS TO BE CHOPPED AS WELL AND ALL OF IT GOES TO
!             THE LITTER/PAPER POOL

              REDUBMAS2=(FCANCMY(I,M)-FCANCMX(I,M))*GRCLAREA(I)  &
                       *ROOTMASS(I,M)*KM2TOM2

!             KEEP ADDING CHOPPED OFF BIOMASS FOR EACH PFT TO GET THE TOTAL
!             FOR A GRID CELL FOR DIAGNOSTICS

              CHOPEDBM(I)=CHOPEDBM(I) + REDUBMAS1 + REDUBMAS2 

!             FIND WHAT'S BURNT, AND WHAT'S CONVERTED TO PAPER &
!             FURNITURE
              COMBUSTC(I,M)=COMBUST(TREATIND(I,M))*REDUBMAS1
              PAPERC(I,M)=PAPER(TREATIND(I,M))*REDUBMAS1 + REDUBMAS2
              FURNTURC(I,M)=FURNITURE(TREATIND(I,M))*REDUBMAS1

!             KEEP ADDING ALL THIS FOR A GIVEN GRID CELL
              GRSUMCOM(I)=GRSUMCOM(I)+COMBUSTC(I,M)
              GRSUMPAP(I)=GRSUMPAP(I)+PAPERC(I,M)
              GRSUMFUR(I)=GRSUMFUR(I)+FURNTURC(I,M)

!             LITTER FROM THE CHOPPED OFF FRACTION OF THE CHOPPED
!             OFF PFT NEEDS TO BE ASSIMILATED, AND SO DOES SOIL C FROM
!             THE CHOPPED OFF FRACTION OF THE CHOPPED PFT

              REDUBMAS1=(FCANCMY(I,M)-FCANCMX(I,M))*GRCLAREA(I) &
                       *LITRMASS(I,M)*KM2TOM2
              INCRLITR(I,M)=REDUBMAS1

              REDUBMAS1=(FCANCMY(I,M)-FCANCMX(I,M))*GRCLAREA(I) &
                       *SOILCMAS(I,M)*KM2TOM2
              INCRSOLC(I,M)=REDUBMAS1

              GRSUMLIT(I)=GRSUMLIT(I)+INCRLITR(I,M)
              GRSUMSOC(I)=GRSUMSOC(I)+INCRSOLC(I,M)
            ENDIF

620       CONTINUE
610     CONTINUE
600   CONTINUE

!     IF BARE FRACTION DECREASES THEN CHOP OFF THE LITTER AND SOIL C
!     FROM THE DECREASED FRACTION AND ADD IT TO GRSUMLIT & GRSUMSOC
!     FOR SPREADING OVER THE WHOLE GRID CELL

      DO 630 I = IL1, IL2
        IF(BAREIORD(I).EQ.-1)THEN

          REDUBMAS1=(PBAREFRA(I)-BAREFRAC(I))*GRCLAREA(I) &
                   *LITRMASS(I,ICC+1)*KM2TOM2

          REDUBMAS2=(PBAREFRA(I)-BAREFRAC(I))*GRCLAREA(I) &
                   *SOILCMAS(I,ICC+1)*KM2TOM2

          GRSUMLIT(I)=GRSUMLIT(I)+REDUBMAS1
          GRSUMSOC(I)=GRSUMSOC(I)+REDUBMAS2
        ENDIF
630   CONTINUE

!     CALCULATE IF THE CHOPPED OFF BIOMASS EQUALS THE SUM OF GRSUMCOM(I),
!     GRSUMPAP(I) & GRSUMFUR(I)

      DO 632 I = IL1, IL2
       IF( ABS(CHOPEDBM(I)-GRSUMCOM(I)-GRSUMPAP(I)-GRSUMFUR(I)).GT. &
          TOLRNCE1 ) THEN
           WRITE(6,*)'AT GRID CELL = ',I
           WRITE(6,*)'CHOPPED BIOMASS DOES NOT EQUALS SUM OF TOTAL'   
           WRITE(6,*)'LUC RELATED EMISSIONS'
           WRITE(6,*)'CHOPEDBM(I) = ',CHOPEDBM(I)
           WRITE(6,*)'GRSUMCOM(I) = ',GRSUMCOM(I)
           WRITE(6,*)'GRSUMPAP(I) = ',GRSUMPAP(I)
           WRITE(6,*)'GRSUMFUR(I) = ',GRSUMFUR(I)
           WRITE(6,*)'SUM OF GRSUMCOM, GRSUMPAP, GRSUMFUR(I) = ', &
            GRSUMCOM(I)+GRSUMPAP(I)+GRSUMFUR(I) 
           CALL XIT('LUC',-11)
       ENDIF
632   CONTINUE     

!     SPREAD CHOPPED OFF STUFF UNIFORMLY OVER THE LITTER AND SOIL C
!     POOLS OF ALL EXISTING PFTS, INCLUDING THE BARE FRACTION.

!     CONVERT THE AVAILABLE C INTO DENSITY 

      DO 640 I = IL1, IL2
        GRDENCOM(I)=GRSUMCOM(I)/(GRCLAREA(I)*KM2TOM2)
        GRDENPAP(I)=GRSUMPAP(I)/(GRCLAREA(I)*KM2TOM2)
        GRDENFUR(I)=GRSUMFUR(I)/(GRCLAREA(I)*KM2TOM2)
        GRDENLIT(I)=GRSUMLIT(I)/(GRCLAREA(I)*KM2TOM2)
        GRDENSOC(I)=GRSUMSOC(I)/(GRCLAREA(I)*KM2TOM2)

640   CONTINUE

      DO 650 J = 1, ICC
        DO 651 I = IL1, IL2
          IF(FCANCMX(I,J).GT.ZERO)THEN
            LITRMASS(I,J)=LITRMASS(I,J)+GRDENPAP(I)+GRDENLIT(I)
            SOILCMAS(I,J)=SOILCMAS(I,J)+GRDENFUR(I)+GRDENSOC(I)
          ELSE
            GLEAFMAS(I,J)=0.0
            BLEAFMAS(I,J)=0.0
            STEMMASS(I,J)=0.0
            ROOTMASS(I,J)=0.0
            LITRMASS(I,J)=0.0
            SOILCMAS(I,J)=0.0
          ENDIF
651     CONTINUE 
650   CONTINUE
 
      DO 660 I = IL1, IL2
        IF(BAREFRAC(I).GT.ZERO)THEN
          LITRMASS(I,ICC+1)=LITRMASS(I,ICC+1)+GRDENPAP(I)+GRDENLIT(I)
          SOILCMAS(I,ICC+1)=SOILCMAS(I,ICC+1)+GRDENFUR(I)+GRDENSOC(I)
        ELSE
          LITRMASS(I,ICC+1)=0.0
          SOILCMAS(I,ICC+1)=0.0
        ENDIF
660   CONTINUE

!     THE COMBUSTED C IS USED TO FIND THE C FLUX THAT WE CAN RELEASE
!     INTO THE ATMOSPHERE.

      DO 670 I = IL1, IL2
        LUCEMCOM(I)=GRDENCOM(I)     ! THIS IS FLUX IN KG C/M2.DAY THAT
!                                   ! WILL BE EMITTED 
!       LUCLTRIN(I)=GRDENPAP(I)+GRDENLIT(I) ! FLUX IN KG C/M2.DAY
!       LUCSOCIN(I)=GRDENFUR(I)+GRDENSOC(I) ! FLUX IN KG C/M2.DAY

        LUCLTRIN(I)=GRDENPAP(I) ! FLUX IN KG C/M2.DAY
        LUCSOCIN(I)=GRDENFUR(I) ! FLUX IN KG C/M2.DAY

!       CONVERT ALL LAND USE CHANGE FLUXES TO U-MOL CO2-C/M2.SEC
        LUCEMCOM(I)=LUCEMCOM(I)*963.62
        LUCLTRIN(I)=LUCLTRIN(I)*963.62
        LUCSOCIN(I)=LUCSOCIN(I)*963.62

670   CONTINUE

!     AND FINALLY WE SEE IF THE TOTAL AMOUNT OF CARBON IS CONSERVED

      DO 700 J = 1, ICC
        DO 701 I = IL1, IL2
          NTOTLMAS(I)=NTOTLMAS(I)+ &
                     (FCANCMX(I,J)*(GLEAFMAS(I,J)+BLEAFMAS(I,J)+&
                     STEMMASS(I,J)+ROOTMASS(I,J))*GRCLAREA(I) &
                     *KM2TOM2)
701     CONTINUE
700   CONTINUE

      DO 710 J = 1, ICC+1
        DO 711 I = IL1, IL2
          IF(J.LT.ICC+1) THEN
            TERM = FCANCMX(I,J)
          ELSE IF(J.EQ.ICC+1) THEN
            TERM = BAREFRAC(I)
          ENDIF
          NTOTDMS1(I)=NTOTDMS1(I)+ &
                     (TERM*LITRMASS(I,J)*GRCLAREA(I)*KM2TOM2) 
          NTOTDMS2(I)=NTOTDMS2(I)+ & 
                     (TERM*SOILCMAS(I,J)*GRCLAREA(I)*KM2TOM2)
711     CONTINUE
710   CONTINUE

      DO 720 I = IL1, IL2
        NTOTCMAS(I)=NTOTLMAS(I)+NTOTDMS1(I)+NTOTDMS2(I)
720   CONTINUE

!     TOTAL LITTER MASS (BEFORE + INPUT FROM CHOPPED OFF BIOMASS)
!     AND AFTER MUST BE SAME

      DO 722 I = IL1, IL2
        IF( ABS(TOTDMAS1(I)+GRSUMPAP(I)-NTOTDMS1(I)).GT.TOLRNCE1 )THEN   
           WRITE(6,*)'AT GRID CELL = ',I
           WRITE(6,*)'TOTAL LITTER CARBON DOES NOT BALANCE AFTER LUC'
           WRITE(6,*)'TOTDMAS1(I) = ',TOTDMAS1(I)
           WRITE(6,*)'GRSUMPAP(I) = ',GRSUMPAP(I)
           WRITE(6,*)'TOTDMAS1(I) + GRSUMPAP(I) = ', &
                     TOTDMAS1(I) + GRSUMPAP(I)
           WRITE(6,*)'NTOTDMS1(I) = ',NTOTDMS1(I)
           CALL XIT('LUC',-12)
        ENDIF
722   CONTINUE

!     FOR CONSERVATION TOTCMASS(I) MUST BE EQUAL TO NTOTCMAS(I) PLUS
!     COMBUSTION CARBON LOSSES

      DO 730 I = IL1, IL2
        IF( ABS(TOTCMASS(I)-NTOTCMAS(I)-GRSUMCOM(I)).GT.TOLRNCE1)THEN
           WRITE(6,*)'AT GRID CELL = ',I
           WRITE(6,*)'TOTAL CARBON DOES NOT BALANCE AFTER LUC'
           WRITE(6,*)'TOTCMASS(I) = ',TOTCMASS(I)
           WRITE(6,*)'NTOTCMAS(I) = ',NTOTCMAS(I)
           WRITE(6,*)'GRSUMCOM(I) = ',GRSUMCOM(I)
           CALL XIT('LUC',-13)
        ENDIF
730   CONTINUE

!     UPDATE GRID AVERAGED VEGETATION BIOMASS, AND LITTER AND SOIL C
!     DENSITIES

      DO 750 J = 1, ICC
        DO 751 I = IL1, IL2
          VGBIOMAS(I)=VGBIOMAS(I)+FCANCMX(I,J)*(GLEAFMAS(I,J)+ &
                     BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J))
          GAVGLTMS(I)=GAVGLTMS(I)+FCANCMX(I,J)*LITRMASS(I,J)
          GAVGSCMS(I)=GAVGSCMS(I)+FCANCMX(I,J)*SOILCMAS(I,J)
751     CONTINUE
750   CONTINUE

      DO 760 I = IL1, IL2
        GAVGLTMS(I)=GAVGLTMS(I)+( BAREFRAC(I)*LITRMASS(I,ICC+1) )
        GAVGSCMS(I)=GAVGSCMS(I)+( BAREFRAC(I)*SOILCMAS(I,ICC+1) )
760   CONTINUE

!     JUST LIKE TOTAL AMOUNT OF CARBON MUST BALANCE, THE GRID AVERAGRED
!     DENSITIES MUST ALSO BALANCE

      DO 780 I = IL1, IL2
       IF( ABS(PVGBIOMS(I)+PGAVLTMS(I)+PGAVSCMS(I)- &
              VGBIOMAS(I)-GAVGLTMS(I)-GAVGSCMS(I)- &
              GRDENCOM(I)).GT.TOLRNCE2 ) THEN
           WRITE(6,*)'IDAY = ',IDAY
           WRITE(6,*)'AT GRID CELL = ',I
           WRITE(6,*)'PBAREFRA(I) = ',PBAREFRA(I)
           WRITE(6,*)'BAREFRAC(I) = ',BAREFRAC(I)
           WRITE(6,*)'PFCANCMX(I,J) = ',(PFCANCMX(I,J),J=1,ICC)
           WRITE(6,*)'NFCANCMX(I,J) = ',(NFCANCMX(I,J),J=1,ICC)
           WRITE(6,*)'TOTAL CARBON DENSITY DOES NOT BALANCE AFTER LUC'
           WRITE(6,*)'PVGBIOMS(I) = ',PVGBIOMS(I)
           WRITE(6,*)'PGAVLTMS(I) = ',PGAVLTMS(I)
           WRITE(6,*)'PGAVSCMS(I) = ',PGAVSCMS(I)
           WRITE(6,*)'VGBIOMAS(I) = ',VGBIOMAS(I)
           WRITE(6,*)'GAVGLTMS(I) = ',GAVGLTMS(I)
           WRITE(6,*)'GAVGSCMS(I) = ',GAVGSCMS(I)
           WRITE(6,*)'GRDENCOM(I) = ',GRDENCOM(I)
           WRITE(6,*)'PVGBIOMS + PGAVLTMS + PGAVSCMS = ', &
                   (PVGBIOMS(I)+PGAVLTMS(I)+PGAVSCMS(I))
           WRITE(6,*)'VGBIOMAS + GAVGLTMS + GAVGSCMS + GRDENCOM = ', &
         (VGBIOMAS(I)+GAVGLTMS(I)+GAVGSCMS(I)+ GRDENCOM(I))
         WRITE(6,*)'DIFF = ',ABS((PVGBIOMS(I)+PGAVLTMS(I)+PGAVSCMS(I)) &
          -(VGBIOMAS(I)+GAVGLTMS(I)+GAVGSCMS(I)+ GRDENCOM(I)))
           WRITE(6,*)'TOLRNCE2 = ',TOLRNCE2
           CALL XIT('LUC',-14)
       ENDIF
780   CONTINUE

      DO 800 J = 1, ICC
        DO 810 I = IL1, IL2
          IF(IDAY.EQ.365)THEN
            PFCANCMX(I,J)=NFCANCMX(I,J)
          ENDIF
810     CONTINUE
800   CONTINUE

      RETURN 

END SUBROUTINE LUC

!=======================================================================

FUNCTION ADJUST_LUC_FRACS(NLAT,NMOS,I,NMTEST,NLTEST,ICC,NFCANCMXROW, &
                          BARE_GROUND_FRAC) RESULT(OUTNFCROW)

! THIS FUNCTION ADJUSTS THE AMOUNT OF EACH PFT TO ENSURE THAT THE FRACTION
! OF GRIDCELL BARE GROUND IS >0.

! J. MELTON, JAN 11 2013

  IMPLICIT NONE

! ARGUMENTS:
INTEGER, INTENT(IN) :: NMOS
INTEGER, INTENT(IN) :: NLAT
INTEGER, INTENT(IN) :: I
INTEGER, INTENT(IN) :: NMTEST
INTEGER, INTENT(IN) :: NLTEST
INTEGER, INTENT(IN) :: ICC
REAL, DIMENSION(NLAT,NMOS,ICC), INTENT(IN) :: NFCANCMXROW
REAL, INTENT(IN) :: BARE_GROUND_FRAC

REAL, DIMENSION(NLAT,NMOS,ICC) :: OUTNFCROW

! LOCAL VARIABLES:
INTEGER :: M, J
REAL, DIMENSION(ICC) :: FRAC_ABV_SEED
REAL :: NEEDED_BARE,REDUCE_PER_PFT,TOT

!-------------------------
TOT = 0.0

! FIND THE AMOUNT OF NEEDED SPACE IN THE OTHER PFTS
! NEED A MINIMUM BARE AREA OF SEED.
NEEDED_BARE=(-BARE_GROUND_FRAC) + SEED

! GET THE PROPORTIONATE AMOUNTS OF EACH PFT ABOVE THE SEED LOWER LIMIT
DO J = 1,NMTEST-1
  M = J
  FRAC_ABV_SEED(J)=MAX(0.0,NFCANCMXROW(I,M,J)-SEED)
  TOT = TOT + FRAC_ABV_SEED(J)
ENDDO

! ADD IN THE BARE GROUND MIN FRACTION OF SEED.
  TOT = TOT + SEED

! NOW REDUCE THE PFTS PROPORTIONAL TO THEIR FRACTIONAL AREA TO MAKE
! THE BARE GROUND FRACTION BE THE SEED AMOUNT AND NO OTHER PFT BE LESS THAN
! SEED
DO J = 1,NMTEST-1
  M = J
  OUTNFCROW(I,M,J)=MAX(SEED,NFCANCMXROW(I,M,J) - NEEDED_BARE * &
                       FRAC_ABV_SEED(J) / TOT)
ENDDO

END FUNCTION ADJUST_LUC_FRACS

END MODULE
