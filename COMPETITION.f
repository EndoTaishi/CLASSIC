      SUBROUTINE COMPETITION(  IDAY,      IL1,       IL2,      ILG,
     2                          ICC, NOL2PFTS,       IC,    NPPVEG, 
     3                        L2MAX, PFTEXIST,  GEREMORT, INTRMORT,
     4                     GLEAFMAS, BLEAFMAS,  STEMMASS, ROOTMASS,
     5                     LITRMASS, SOILCMAS,  GRCLAREA,   LAMBDA,
     6                      BMASVEG,   DELTAT,   BURNVEG,     SORT,
C    ------------------- INPUTS ABOVE THIS LINE -------------------
     7                      FCANCMX,   FCANMX,  VGBIOMAS, GAVGLTMS,
     8                     GAVGSCMS,
C    ------------------- UPDATES ABOVE THIS LINE ------------------
     9                     ADD2ALLO,        C,        M) 
C    ------------------- OUTPUTS ABOVE THIS LINE ------------------
C
C               CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
C                          PFT COMPETITION SUBROUTINE 
C
C     17  OCT 2012  - ADAPT SUBROUTINE TO ANY NUMBER OF CROPS OR GRASS 
C     J. MELTON       PFTS
C
C     27  MAY 2004  - THIS SUBROUTINE CALCULATES THE COMPETITION BETWEEN
C     V. ARORA        PFTs BASED ON LOTKA-VOLTERRA EQNS. OT ITS MODIFIED
C                     FORMS. EITHER OPTION MAY BE USED.
C
C                     PFTs THAT MAY EXIST ARE ALLOWED TO COMPETE FOR
C                     AVAILABLE SPACE IN A GRID CELL. PFTs THAT CAN'T
C                     EXIST BASED ON LONG TERM BIOCLIMATIC PARAMETERS
C                     ARE SLOWLY KILLED BY INCREASING THEIR MORTALITY.               
C
C     INPUTS 
C
C     IDAY      - DAY OF THE YEAR
C     ILG       - NO. OF GRID CELLS IN LATITUDE CIRCLE
C     IL1,IL2   - IL1=1, IL2=ILG
C     ICC       - NUMBER OF CTEM PFTs
C     NOL2PFTS  - NUMBER OF LEVEL 2 CTEM PFTs
C     IC        - NUMBER OF CLASS PFTs
C     NPPVEG    - NPP FOR EACH PFT TYPE /M2 OF VEGETATED AREA 
C                 u-MOL CO2-C/M2.SEC
C     L2MAX     - MAXIMUM NO. OF LEVEL 2 PFTs
C     PFTEXIST  - BINARY ARRAY INDICATING PFTs EXIST (=1) OR NOT (=0)
C     GEREMORT  - GROWTH RELATED MORTALITY (1/DAY)
C     INTRMORT  - INTRINSIC (AGE RELATED) MORTALITY (1/DAY)
C     GLEAFMAS  - GREEN LEAF MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     BLEAFMAS  - BROWN LEAF MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     STEMMASS  - STEM MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     ROOTMASS  - ROOT MASS FOR EACH OF THE 9 CTEM PFTs, Kg C/M2
C     LITRMASS  - LITTER MASS FOR EACH OF THE 9 CTEM PFTs + BARE, Kg C/M2
C     SOILCMAS  - SOIL CARBON MASS FOR EACH OF THE 9 CTEM PFTs
C                 + BARE, Kg C/M2
C     GRCLAREA  - GRID CELL AREA, KM^2
C     LAMBDA    - FRACTION OF NPP THAT IS USED FOR SPATIAL EXPANSION
C     BMASVEG   - TOTAL (GLEAF + STEM + ROOT) BIOMASS FOR EACH CTEM PFT,
C                 Kg C/M2
C     DELTAT    - CTEM TIME STEP, 1 DAY 
C     BURNVEG   - AREAS BURNED, KM^2, FOR 9 CTEM PFTs
C
C     UPDATES AND OUTPUTS
C
C     FCANCMX   - FRACTIONAL COVERAGE OF CTEM's 9 PFTs
C     FCANMX    - FRACTIONAL COVERAGE OF CLASS' 4 PFTs
C     VGBIOMAS  - GRID AVERAGED VEGETATION BIOMASS, KG C/M2
C     GAVGLTMS  - GRID AVERAGED LITTER MASS, KG C/M2
C     GAVGSCMS  - GRID AVERAGED SOIL C MASS, KG C/M2
C     ADD2ALLO  - NPP KG C/M2.DAY THAT IS USED FOR EXPANSION AND
C                 SUBSEQUENTLY ALLOCATED TO LEAVES, STEM, AND ROOT VIA 
C                 THE ALLOCATION PART OF THE MODEL.
C 
      IMPLICIT NONE
C
C
      INTEGER I, J, IDAY, IL1, IL2, ILG, ICC, IC, KK, PFTEXIST(ILG,ICC)
      INTEGER NUMCROPS   ! NUMBER OF CROP PFTS
      INTEGER NUMGRASS   ! NUMBER OF GRASS PFTS
      INTEGER GRASSONE   ! INDEX OF THE FIRST GRASS PFT
      INTEGER GRASSTWO   ! INDEX OF THE SECOND GRASS PFT
C
      PARAMETER (KK=12)  ! PRODUCT OF CLASS PFTs AND L2MAX
C
      INTEGER N, K, INIRANK(ICC-2), T1(ILG), RANK(ILG,ICC-2), K1, K2, L,
     1       EXIST1(ILG,ICC-2),      USEEXIST(ILG,ICC-2),    SORT(ICC)
C
      INTEGER    NOL2PFTS(IC),              L2MAX,             SDFRACIN,
     1      FRACIORD(ILG,ICC),      BAREIORD(ILG),           A,   B,  G
C
      LOGICAL   CROP(ICC), GRASS(ICC)
C
      REAL  GEREMORT(ILG,ICC),  INTRMORT(ILG,ICC),        GRCLAREA(ILG),
     1        NPPVEG(ILG,ICC),   BMASVEG(ILG,ICC),     BURNVEG(ILG,ICC),
     2      STEMMASS(ILG,ICC),  ROOTMASS(ILG,ICC),    GLEAFMAS(ILG,ICC),
     3      BLEAFMAS(ILG,ICC),LITRMASS(ILG,ICC+1),  SOILCMAS(ILG,ICC+1)
C
      REAL   FCANCMX(ILG,ICC),     FCANMX(ILG,IC)
C          
      REAL               ZERO,        BIO2SAP(KK),            BIOCLIMRT,
     &            SEEDFRAC(2),           TOLRANC1,              MINBARE,
     &               TOLRANC2
C
      REAL    LAMBDA(ILG,ICC),          C(ILG,ICC),          M(ILG,ICC),
     1      MRTBOCLM(ILG,ICC),   USENPPVG(ILG,ICC),
     2              TEMP(ILG),  USEFRAC(ILG,ICC-2),     USEC(ILG,ICC-2),
     3        USEM(ILG,ICC-2),     FRAC(ILG,ICC-2),       C1(ILG,ICC-2),
     4          M1(ILG,ICC-2),    SUM1, SUM2, SUM3,    TERM2(ILG,ICC-2),
     5       TERM3(ILG,ICC-2),    TERM4(ILG,ICC-2),  COLTERM(ILG,ICC-2),
     6   DEATHTERM(ILG,ICC-2),      TERM,     SUM4,  DELFRAC(ILG,ICC-2),
     7          CROPFRAC(ILG),        VEGFRAC(ILG),   CHNGFRAC(ILG,ICC),
     8      EXPNTERM(ILG,ICC),   MORTTERM(ILG,ICC),   ADD2ALLO(ILG,ICC),
     9      PGLFMASS(ILG,ICC),   PBLFMASS(ILG,ICC),   PROTMASS(ILG,ICC),
     A      PSTMMASS(ILG,ICC),   PFCANCMX(ILG,ICC),             COLMULT,
     B          MINCFRAC(ILG),   BIOMASVG(ILG,ICC),  PBIOMASVG(ILG,ICC),
     C      PUTASIDE(ILG,ICC),   NPPVEGAR(ILG,ICC), PLTRMASS(ILG,ICC+1),
     D    PSOCMASS(ILG,ICC+1), DEADMASS(ILG,ICC+1), PDEADMAS(ILG,ICC+1),
     E          BAREFRAC(ILG),   USEBMSVG(ILG,ICC),  OWNSOLC(ILG,ICC+1),
     F      BARELITR(ILG,ICC),              DELTAT,  OWNLITR(ILG,ICC+1),
     G      BARESOLC(ILG,ICC), INCRLITR(ILG,ICC+1), INCRSOLC(ILG,ICC+1) 
C
      REAL      VGBIOMAS(ILG),       GAVGLTMS(ILG),       GAVGSCMS(ILG),
     1               BEFRMASS,            AFTRMASS,       PVGBIOMS(ILG),
     2          PGAVLTMS(ILG),       PGAVSCMS(ILG),       ADD2DEAD(ILG),
     3          GAVGPUTA(ILG),        GAVGNPP(ILG),       PBAREFRA(ILG),
     4          GRSUMLIT(ILG),       GRSUMSOC(ILG),   PFTAREAB(ILG,ICC),
     5      PFTAREAA(ILG,ICC)         
C
      LOGICAL LOTVOL, ARORA, BOER
C
C     ------------------------------------------------------------------
C                           PARAMETERS USED 
C
C     NOTE THE STRUCTURE OF PARAMETER VECTORS WHICH CLEARLY SHOWS THE
C     CLASS PFTs (ALONG ROWS) AND CTEM SUB-PFTs (ALONG COLUMNS)
C
C     NEEDLE LEAF |  EVG1      EVG2      DCD
C     BROAD LEAF  |  EVG   DCD-CLD   DCD-DRY
C     CROPS       |   C3        C4       ---
C     GRASSES     |   C3        C4       ---
C
C     MULTIPLYING FACTOR FOR CONVERTING BIOMASS DENSITY TO SAPLING 
C     DENSITY
      DATA BIO2SAP/0.10, 0.10, 0.00,
     &             0.10, 0.10, 0.10,
     &             0.10, 0.10, 0.00,
     &             0.10, 0.10, 0.00/

C     SIMPLE CROP MATRIX, DEFINE THE NUMBER AND POSITION OF THE CROPS
      CROP=.FALSE.
      CROP(6)=.TRUE.
      CROP(7)=.TRUE.   
      NUMCROPS=2   

C     SIMPLE GRASS MATRIC, DEFINE THE NUMBER AND POSITION OF GRASS
      GRASS=.FALSE.
      GRASS(8)=.TRUE.
      GRASS(9)=.TRUE.
      GRASSONE=8
      GRASSTWO=9
      NUMGRASS=2

C     MORTALITY RATE (1/YEAR) FOR PFTs THAT NO LONGER EXIST WITHIN THEIR
C     PRE-DEFINED BIOCLIMATIC RANGE
      DATA BIOCLIMRT/0.25/
C
C     SEEDING FRACTION FOR LOTKA-VOLTERRA AND ARORA-BOER SCHEME
      DATA SEEDFRAC/0.001,0.001/
C
C     ERROR TOLERANCE FOR C BALANCE FOR EACH PFT OVER GCM GRID CELL
      DATA TOLRANC1/0.150/  ! KG C
      DATA TOLRANC2/0.0050/ ! KG C/M2
C
C     MINIMUM BARE FRACTION
      DATA MINBARE/0.001/  
C
C     ZERO
      DATA ZERO/1.0E-20/
C     ---------------------------------------------------------------
C
      IF(ICC.NE.9)                      CALL XIT('COMPETITION',-1)
      IF(IC.NE.4)                       CALL XIT('COMPETITION',-2)
      IF(L2MAX.NE.3)                    CALL XIT('COMPETITION',-3)
C
C     SET DESIRED MODEL TO BE USED TO .TRUE. AND ALL OTHER TO .FALSE.
C
      LOTVOL=.FALSE.  ! ORIGINAL LOTKA-VOLTERRA EQNS.
      ARORA =.TRUE.  ! MODIFIED FORM OF LV EQNS WITH F MISSING
C      LOTVOL=.TRUE.
C       ARORA =.FALSE.
C       BOER  =.TRUE.
      BOER  =.FALSE.   ! MODIFIED FORM OF LV EQNS WITH F MISSING AND
C                     ! A MODIFIED SELF-THINNING TERM
C
C     SET COMPETITION PARAMETERS ACCORDING TO THE MODEL CHOSEN
C
      IF(LOTVOL .AND. (.NOT.ARORA). AND. (.NOT.BOER))THEN
        A=1 ! ALPHA. THIS IS THE B IN THE ARORA & BOER (2006) PAPER
        B=1 ! BETA
        G=0 ! GAMMA
        COLMULT=4.00  ! MULTIPLIER FOR COLONIZATION RATE
        SDFRACIN=1   ! SEED FRACTION INDEX
      ELSE IF(ARORA .AND. (.NOT.LOTVOL). AND. (.NOT.BOER))THEN
        A=0 ! ALPHA
        B=1 ! BETA
        G=0 ! GAMMA
        COLMULT=1.00 ! MULTIPLIER FOR COLONIZATION RATE
        SDFRACIN=2   ! SEED FRACTION INDEX
      ELSE IF(BOER.AND. (.NOT.LOTVOL). AND. (.NOT.ARORA))THEN
        A=0 ! ALPHA
        B=1 ! BETA
        G=1 ! GAMMA
        COLMULT=1.00 ! MULTIPLIER FOR COLONIZATION RATE
        SDFRACIN=2   ! SEED FRACTION INDEX
      ELSE
        WRITE(6,*)'FOOL! CHOOSE COMPETITION MODEL PROPERLY'
        CALL XIT('COMPETITION',-4)
      ENDIF
C
C     ---------------------------------------------------------------
C
C     UPDATE FRACTIONAL COVERAGES OF PFTs TO TAKE INTO ACCOUNT THE AREA
C     BURNT BY FIRE. ADJUST ALL POOLS WITH NEW DENSITIES IN THEIR NEW
C     AREAS AND INCREASE BARE FRACTION.
C
C     AND WHILE WE ARE DOING THIS ALSO RUN A SMALL CHECK TO MAKE SURE
C     GRID AVERAGED QUANTITIES DO NOT GET MESSED UP.
C
      DO 1200 I = IL1, IL2
        PVGBIOMS(I)=VGBIOMAS(I)
        PGAVLTMS(I)=GAVGLTMS(I)
        PGAVSCMS(I)=GAVGSCMS(I)
        PBAREFRA(I)=1.0
        BAREFRAC(I)=1.0
        VGBIOMAS(I)=0.0
        GAVGLTMS(I)=0.0
        GAVGSCMS(I)=0.0
1200  CONTINUE
C
C     INITIAL RANK/SUPERIORITY ORDER FOR SIMULATING COMPETITION. SINCE
C     CROPS ARE NOT IN COMPETITION THEIR RANK DOESN'T MATTER AND
C     THEREFORE WE ONLY HAVE ICC-2 RANKS CORRESPONDING TO THE REMAINING 
C     PFTs. THE FIRST ICC-4 ARE TREE PFTs AND THE LAST TWO ARE THE C3 AND C4
C     GRASSES.
      DO 1210 J = 1,ICC-NUMCROPS
      INIRANK(J)=J
1210  CONTINUE

      DO 1220 J = 1, ICC
       IF(.NOT. CROP(J))THEN  ! DO NOT RUN FOR CROPS
        DO 1230 I = IL1, IL2
          PBAREFRA(I)=PBAREFRA(I)-FCANCMX(I,J)
          PFTAREAB(I,J)=(FCANCMX(I,J)*GRCLAREA(I))
          PFTAREAA(I,J)=(FCANCMX(I,J)*GRCLAREA(I))-BURNVEG(I,J)
          FCANCMX(I,J)=MAX( SEEDFRAC(SDFRACIN), 
     &                      (PFTAREAA(I,J)/GRCLAREA(I)) )
          PFTAREAA(I,J)=FCANCMX(I,J)*GRCLAREA(I)
          BAREFRAC(I)=BAREFRAC(I)-FCANCMX(I,J)
          IF(FCANCMX(I,J).GT.ZERO)THEN
            TERM = PFTAREAB(I,J)/PFTAREAA(I,J)
            GLEAFMAS(I,J)=GLEAFMAS(I,J)*TERM
            BLEAFMAS(I,J)=BLEAFMAS(I,J)*TERM
            STEMMASS(I,J)=STEMMASS(I,J)*TERM
            ROOTMASS(I,J)=ROOTMASS(I,J)*TERM
            LITRMASS(I,J)=LITRMASS(I,J)*TERM
            SOILCMAS(I,J)=SOILCMAS(I,J)*TERM
          ELSE
            GLEAFMAS(I,J)=0.0
            BLEAFMAS(I,J)=0.0
            STEMMASS(I,J)=0.0
            ROOTMASS(I,J)=0.0
            LITRMASS(I,J)=0.0
            SOILCMAS(I,J)=0.0
          ENDIF
1230    CONTINUE
       ENDIF
1220  CONTINUE
C
      DO 1240 I = IL1, IL2
       DO 1250 J=1,ICC
         IF (GRASS(J)) THEN
          PBAREFRA(I)=PBAREFRA(I)-FCANCMX(I,J)
          BAREFRAC(I)=BAREFRAC(I)-FCANCMX(I,J)
         ENDIF
1250   CONTINUE 
        IF(BAREFRAC(I).GT.ZERO)THEN
          TERM=PBAREFRA(I)/BAREFRAC(I)
          LITRMASS(I,ICC+1) = LITRMASS(I,ICC+1)*TERM
          SOILCMAS(I,ICC+1) = SOILCMAS(I,ICC+1)*TERM
        ELSE
          LITRMASS(I,ICC+1) = 0.0
          SOILCMAS(I,ICC+1) = 0.0
        ENDIF
1240  CONTINUE
C
C     CHECK IF TOTAL BIOMASS IS SAME BEFORE AND AFTER ADJUSTING
C     FRACTIONS
C
      DO 1260 J = 1, ICC
        DO 1270 I = IL1, IL2
          VGBIOMAS(I)=VGBIOMAS(I)+FCANCMX(I,J)*(GLEAFMAS(I,J)+
     &     BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J))
          GAVGLTMS(I)=GAVGLTMS(I)+FCANCMX(I,J)*LITRMASS(I,J)
          GAVGSCMS(I)=GAVGSCMS(I)+FCANCMX(I,J)*SOILCMAS(I,J)
1270    CONTINUE
1260  CONTINUE
C
      DO 1280 I = IL1, IL2
        GAVGLTMS(I)=GAVGLTMS(I)+ BAREFRAC(I)*LITRMASS(I,ICC+1)
        GAVGSCMS(I)=GAVGSCMS(I)+ BAREFRAC(I)*SOILCMAS(I,ICC+1)
1280  CONTINUE
C
      DO 1300 I = IL1, IL2
C
        IF(ABS(VGBIOMAS(I)-PVGBIOMS(I)).GT.TOLRANC1)THEN
          WRITE(6,*)'GRID AVERAGED BIOMASS DENSITIES DO NOT BALANCE'
          WRITE(6,*)'AFTER FRACTIONAL COVERAGES ARE CHANGED TO TAKE'
          WRITE(6,*)'INTO ACCOUNT BURN AREA'
          WRITE(6,*)'VGBIOMAS(',I,')=',VGBIOMAS(I)
          WRITE(6,*)'PVGBIOMS(',I,')=',PVGBIOMS(I)
          CALL XIT('COMPETITION',-11)
        ENDIF
C
        IF(ABS(GAVGLTMS(I)-PGAVLTMS(I)).GT.TOLRANC1)THEN
          WRITE(6,*)'GRID AVERAGED BIOMASS DENSITIES DO NOT BALANCE'
          WRITE(6,*)'AFTER FRACTIONAL COVERAGES ARE CHANGED TO TAKE'
          WRITE(6,*)'INTO ACCOUNT BURN AREA'
          WRITE(6,*)'GAVGLTMS(',I,')=',GAVGLTMS(I)
          WRITE(6,*)'PGAVLTMS(',I,')=',PGAVLTMS(I)
          CALL XIT('COMPETITION',-12)
        ENDIF
C
        IF(ABS(GAVGSCMS(I)-PGAVSCMS(I)).GT.TOLRANC1)THEN
          WRITE(6,*)'GRID AVERAGED BIOMASS DENSITIES DO NOT BALANCE'
          WRITE(6,*)'AFTER FRACTIONAL COVERAGES ARE CHANGED TO TAKE'
          WRITE(6,*)'INTO ACCOUNT BURN AREA'
          WRITE(6,*)'GAVGSCMS(',I,')=',GAVGSCMS(I)
          WRITE(6,*)'PGAVSCMS(',I,')=',PGAVSCMS(I)
          CALL XIT('COMPETITION',-13)
        ENDIF
C
1300  CONTINUE
C
C     ---------------------------------------------------------------
C     NOW WE DO OUR USUAL INITIALIZATION
C
      DO 150 J = 1, ICC
        DO 160 I = IL1, IL2
          C(I,J)=0.0        ! COLONIZATION RATE
          M(I,J)=0.0        ! MORTALITY RATE
          MRTBOCLM(I,J)=0.0 ! MORTALITY RATE IF LONG-TERM BIOCLIMATIC 
C                           ! CONDITIONS BECOME UNFAVOURABLE
          USENPPVG(I,J)=0.0
          CHNGFRAC(I,J)=0.0
          EXPNTERM(I,J)=0.0
          MORTTERM(I,J)=0.0
          ADD2ALLO(I,J)=0.0
          PGLFMASS(I,J)=GLEAFMAS(I,J) ! SAVE ALL BIOMASSES BEFORE MAKING
          PBLFMASS(I,J)=BLEAFMAS(I,J) ! CHANGES SO THAT WE CAN MAKE SURE
          PROTMASS(I,J)=ROOTMASS(I,J) ! MASS BALANCE IS PRESERVED.
          PSTMMASS(I,J)=STEMMASS(I,J)
          PLTRMASS(I,J)=LITRMASS(I,J)
          PSOCMASS(I,J)=SOILCMAS(I,J)
          PFCANCMX(I,J)=FCANCMX(I,J)
          BIOMASVG(I,J)=0.0
          PBIOMASVG(I,J)=0.0
          NPPVEGAR(I,J)=0.0
          DEADMASS(I,J)=0.0
          PDEADMAS(I,J)=0.0
          BARELITR(I,J)=0.0    ! Kg C OF LITTER ADDED TO BARE FRACTION
          BARESOLC(I,J)=0.0    ! AND SAME FOR SOIL C
          FRACIORD(I,J)=0
          INCRLITR(I,J)=0.0
          INCRSOLC(I,J)=0.0
          OWNLITR(I,J)=0.0
          OWNSOLC(I,J)=0.0
160     CONTINUE
150   CONTINUE
C
      DO 170 I = IL1, IL2
        CROPFRAC(I)=0.0
        VEGFRAC(I)=0.0
        TEMP(I)=0.0
        T1(I)=0
        MINCFRAC(I)=0.0
        BAREFRAC(I)=1.0
        PBAREFRA(I)=1.0
        PVGBIOMS(I)=VGBIOMAS(I)  ! STORE GRID AVERAGE QUANTITIES IN
        PGAVLTMS(I)=GAVGLTMS(I)  ! TEMPORARY ARRAYS
        PGAVSCMS(I)=GAVGSCMS(I)
        VGBIOMAS(I)=0.0
        GAVGLTMS(I)=0.0
        GAVGSCMS(I)=0.0
        PLTRMASS(I,ICC+1)=LITRMASS(I,ICC+1)
        PSOCMASS(I,ICC+1)=SOILCMAS(I,ICC+1)
        DEADMASS(I,ICC+1)=0.0
        PDEADMAS(I,ICC+1)=0.0
        ADD2DEAD(I)=0.0
        GAVGPUTA(I)=0.0 ! GRID AVERAGED VALUE OF C PUT ASIDE FOR ALLOCATION
        GAVGNPP(I)=0.0  ! GRID AVERAGED NPP Kg C/M2 FOR BALANCE PURPOSES
        BAREIORD(I)=0
        GRSUMLIT(I)=0.0
        GRSUMSOC(I)=0.0
        OWNLITR(I,ICC+1)=0.0
        OWNSOLC(I,ICC+1)=0.0
        INCRLITR(I,ICC+1)=0.0
        INCRSOLC(I,ICC+1)=0.0
170   CONTINUE
C
      DO 180 J = 1, ICC-NUMCROPS
        DO 181 I = IL1, IL2
          RANK(I,J)=J
          FRAC(I,J)=0.0
          C1(I,J)=0.0
          M1(I,J)=0.0
          USEFRAC(I,J)=0.0
          USEC(I,J)=0.0
          USEM(I,J)=0.0
          COLTERM(I,J)=0.0
          DEATHTERM(I,J)=0.0
          TERM2(I,J)=0.0
          TERM3(I,J)=0.0
          TERM4(I,J)=0.0
          DELFRAC(I,J)=0.0
          EXIST1(I,J)=0
          USEEXIST(I,J)=0
181     CONTINUE
180   CONTINUE
C
C     ESTIMATE COLONIZATION AND MORTALITY RATE FOR EACH PFT, EXCEPT FOR
C     CROPS WHOSE FRACTIONAL COVERAGE IS PRESCRIBED.
C
      DO 200 J = 1, ICC
       IF(.NOT. CROP(J))THEN  ! DO NOT RUN FOR CROPS
        DO 210 I = IL1, IL2
C
C         COLONIZATION RATE (1/DAY). THE FACTOR (DELTAT/963.62) CONVERTS
C         NPP FROM u-MOL CO2-C/M2.SEC -> Kg C/M2.DAY
C
          USEBMSVG(I,J)= MIN(5.0, MAX(0.25, BMASVEG(I,J)))
C
          C(I,J)=LAMBDA(I,J)*MAX(0.0,NPPVEG(I,J))*(DELTAT/963.62)*
     &           COLMULT*(1.0/(BIO2SAP(SORT(J))*USEBMSVG(I,J)))
C
C         MORTALITY RATE IS THE SUM OF GROWTH RELATED MORTALITY,
C         INTRINSIC MORTALITY, AND AN ADDITIONAL MORTALITY THAT KICKS IN
C         WHEN LONG TERM AVERAGED BIOCLIMATIC CONDITIONS BECOME
C         UNFAVOURABLE FOR A PFT. THIS LAST TERM IS BASED ON THE
C         BINARY ARRAY PFTEXIST.
C
          IF(PFTEXIST(I,J).EQ.0)THEN
            MRTBOCLM(I,J)=BIOCLIMRT/365.0
          ELSE IF(PFTEXIST(I,J).EQ.1)THEN
            MRTBOCLM(I,J)=0.0
          ENDIF
C
          M(I,J)=GEREMORT(I,J)+INTRMORT(I,J)+MRTBOCLM(I,J)
C
210     CONTINUE
       ENDIF
200   CONTINUE
C
C    ---> FROM HERE ON WE ASSUME THAT WE ONLY HAVE ICC-NUMCROPS PFTs <----
C              SINCE CROPS ARE NOT PART OF THE COMPETITION.
C
C     BASED ON NPP FOR EACH PFT FIND THE COMPETITION RANKS / SUPERIORITY 
C     ORDER FOR SIMULATING COMPETITION. NOTE THAT CROPS
C     ARE NOT IN COMPETITION, SO THE COMPETITION IS BETWEEN THE
C     REMAINING PFTs. IN ADDITION PFTs WHICH SHOULDN'T EXIST IN THE
C     GRID CELL BECAUSE OF UNFAVOURABLE VALUES OF LONG-TERM CLIMATIC
C     CONDITIONS ARE CONSIDERED INFERIOR.
C
C     FIND CROP FRACTION
C
      CROPFRAC=0.0
      DO 220 J = 1, ICC
        IF (CROP(J)) THEN
         DO 221 I = IL1, IL2
          CROPFRAC(I)=CROPFRAC(I)+FCANCMX(I,J)
221      CONTINUE
        ENDIF
220   CONTINUE
C
C     RANK THE TREE PFTs ACCORDING TO THEIR COLONIZATION RATES 
C
      DO 250 J = 1, ICC
        DO 251 I = IL1, IL2
          USENPPVG(I,J)=C(I,J)*PFTEXIST(I,J)
251     CONTINUE
250   CONTINUE
C
      DO 260 J = 1, ICC-NUMCROPS
        DO 261 I = IL1, IL2
          RANK(I,J)=INIRANK(J)
261     CONTINUE
260   CONTINUE
C
C     BUBBLE SORT ACCORDING TO COLONIZATION RATES
C
      DO 270 J = 1, ICC-NUMCROPS-NUMGRASS
        DO 280 N = 1, ICC-NUMCROPS-NUMGRASS
          DO 290 I = IL1, IL2
            IF(USENPPVG(I,N).LT.USENPPVG(I,J))THEN
              TEMP(I)=USENPPVG(I,N)
              USENPPVG(I,N)=USENPPVG(I,J)
              USENPPVG(I,J)=TEMP(I)
              T1(I)=RANK(I,N)
              RANK(I,N)=RANK(I,J)
              RANK(I,J)=T1(I)
            ENDIF
290       CONTINUE
280     CONTINUE
270   CONTINUE
C
C     THE RANK OF C3 AND C4 GRASS IS ALSO DETERMINED ON THE BASIS OF
C     THEIR NPP BUT GRASSES ARE ALWAYS ASSUMED TO BE INFERIOR TO TREE
C     PFTs
C
      DO 310 I = IL1, IL2
        IF(USENPPVG(I,GRASSONE).GE.USENPPVG(I,GRASSTWO))THEN 
          RANK(I,GRASSONE)=GRASSONE
          RANK(I,GRASSTWO)=GRASSTWO
        ELSE IF(USENPPVG(I,GRASSTWO).LT.USENPPVG(I,GRASSTWO))THEN
          RANK(I,GRASSONE)=GRASSTWO
          RANK(I,GRASSTWO)=GRASSONE
        ENDIF
310   CONTINUE
C
C     WITH THE RANKS OF ALL PFTs IN ALL GRID CELLS WE CAN NOW SIMULATE
C     COMPETITION BETWEEN THEM. FOR LOTKA-VOLTERRA EQNS WE NEED A
C     MINIMUM SEEDING FRACTION OTHERWISE THE PFTs WILL NOT EXPAND AT
C     ALL.
C
      DO 330 J = 1, ICC-NUMCROPS   ! J NOW GOES FROM 1 TO ICC-NUMCROPS
        IF (.NOT. CROP(J)) THEN !IF(J.LE.6)THEN  
          N=J
        ELSE
          N=J+NUMCROPS
        ENDIF 
        DO 340 I = IL1, IL2
          FRAC(I,J)=MAX(SEEDFRAC(SDFRACIN), FCANCMX(I,N))
          EXIST1(I,J)=PFTEXIST(I,N)
          C1(I,J)=C(I,N)*PFTEXIST(I,N)
          M1(I,J)=M(I,N)
340     CONTINUE
330   CONTINUE
C
C     ARRANGE COLONIZATION AND MORTALITY RATES, AND FRACTIONS, ACCORDING
C     TO SUPERIORITY RANKS
C
      DO 350 N = 1, ICC-NUMCROPS   ! N NOW GOES FROM 1 TO ICC-NUMCROPS
        DO 360 I = IL1, IL2
          USEFRAC(I,N)=FRAC(I,RANK(I,N))
          USEC(I,N)=C1(I,RANK(I,N))
          USEM(I,N)=M1(I,RANK(I,N))
          USEEXIST(I,N)=EXIST1(I,RANK(I,N))
360     CONTINUE
350   CONTINUE
C
      DO 400 N = 1, ICC-NUMCROPS   ! N NOW GOES FROM 1 TO ICC-NUMCROPS
        DO 410 I = IL1, IL2
C
          COLTERM(I,N)=USEC(I,N)*(USEFRAC(I,N)**A) ! COLONIZATION TERM
C
          SUM1 = CROPFRAC(I)+MINBARE
          DO 420 K = 1, N-1, 1
            SUM1 = SUM1 + USEFRAC(I,K)
420       CONTINUE          
C
          TERM2(I,N)=USEC(I,N)*(USEFRAC(I,N)**A)*  ! SELF & EXPANSION THINNING
     &               (SUM1+(USEFRAC(I,N)**B)) 
          TERM3(I,N)=USEM(I,N)*USEFRAC(I,N) ! MORTALITY TERM
C
          SUM2 = 0.0
          DO 430 J = 1, N-1, 1
            SUM3 = CROPFRAC(I)
            DO 440 K = 1, J-1, 1
              SUM3 = SUM3 + USEFRAC(I,K)
440         CONTINUE
            SUM4 = CROPFRAC(I)
            DO 450 K = 1, J, 1
              SUM4 = SUM4 + USEFRAC(I,K)
450         CONTINUE
            SUM2 = SUM2 + (
     &      ( ((1.-SUM3)**G)*USEC(I,J)*(USEFRAC(I,J)**A)*USEFRAC(I,N) )/
     &      ( (1.-SUM4)**G )  )
430       CONTINUE
          TERM4(I,N)=SUM2  ! INVASION
          DEATHTERM(I,N) = TERM2(I,N) + TERM3(I,N) + TERM4(I,N)
          DELFRAC(I,N)=COLTERM(I,N)-DEATHTERM(I,N) ! DELTA FRACTION
C
410     CONTINUE
400   CONTINUE
C
C     UPDATE FRACTIONS AND CHECK IF ALL FRACTIONS ARE +VE 
C
      DO 500 N = 1, ICC-NUMCROPS
        DO 510 I = IL1, IL2
          USEFRAC(I,N)=USEFRAC(I,N)+DELFRAC(I,N)
          IF(USEFRAC(I,N).LT.0.0)THEN
            WRITE(6,*)'FRACTIONAL COVERAGE -VE FOR CELL ',I,' AND PFT',
     &      N
            CALL XIT('COMPETITION',-5)
          ENDIF
          USEFRAC(I,N)=MAX(SEEDFRAC(SDFRACIN),USEFRAC(I,N))
510     CONTINUE
500   CONTINUE
C
C     WITH THE MINIMUM SEEDING FRACTION PRESCRIPTION, ESPECIALLY FOR
C     LOTKA VOLTERRA EQNS THE TOTAL VEG FRACTION MAY EXCEED 1. TO
C     PREVENT THIS WE NEED TO ADJUST FRACTIONAL COVERAGE OF ALL NON-CROP
C     PFTS THAT DO NOT HAVE THE MINIMUM FRACTION.
C
      DO 530 I = IL1, IL2
        VEGFRAC(I)=CROPFRAC(I)+ SEEDFRAC(SDFRACIN)  !TOTAL VEGETATION FRACTION
        MINCFRAC(I)=CROPFRAC(I)+ SEEDFRAC(SDFRACIN) !SUM OF MININUM PRESCRIBED & CROP FRACTIONS
530   CONTINUE
C
      DO 540 N = 1, ICC-NUMCROPS
        DO 541 I = IL1, IL2
          VEGFRAC(I)=VEGFRAC(I)+USEFRAC(I,N)
          IF(ABS(USEFRAC(I,N)-SEEDFRAC(SDFRACIN)).LE.ZERO)
     &    THEN
            MINCFRAC(I)= MINCFRAC(I)+ USEFRAC(I,N)
          ENDIF 
541     CONTINUE
540   CONTINUE
C
      DO 550 N = 1, ICC-NUMCROPS
        DO 551 I = IL1, IL2
          IF(VEGFRAC(I).GT.1.0.AND.
     &    ABS(USEFRAC(I,N)-SEEDFRAC(SDFRACIN)).GT.ZERO)
     &    THEN
            TERM =(1.-MINCFRAC(I))/(VEGFRAC(I)-MINCFRAC(I)) 
            USEFRAC(I,N)=USEFRAC(I,N)*TERM
          ENDIF
551     CONTINUE
550   CONTINUE
C
C     CHECK AGAIN THAT TOTAL VEG FRAC DOESN'T EXCEED 1.
C
      DO 560 I = IL1, IL2
        VEGFRAC(I)=CROPFRAC(I)+ SEEDFRAC(SDFRACIN)  !TOTAL VEGETATION FRACTION
560   CONTINUE
C
      DO 570 N = 1, ICC-NUMCROPS
        DO 571 I = IL1, IL2
          VEGFRAC(I)=VEGFRAC(I)+USEFRAC(I,N)
571     CONTINUE
570   CONTINUE
C
      DO 580 I = IL1, IL2
        IF(VEGFRAC(I).GT.1.0)THEN
          WRITE(6,*)'VEGETATION FRACTION IN CELL ',I,' GREATER THAN'
          WRITE(6,*)'1.0 AND EQUAL TO ',VEGFRAC(I) 
          CALL XIT('COMPETITION',-6)
        ENDIF
580   CONTINUE
C
C     MAP DELFRAC TO CHNGFRAC SO THAT WE GET CHANGE IN FRACTION
C     CORRESPONDING TO THE ACTUAL 10 PFTs
C
      DO 590 J = 1, ICC-NUMCROPS   ! J NOW GOES FROM 1 TO 8
        DO 591 I = IL1, IL2
C
          IF(RANK(I,J).LE.6)THEN
            K=RANK(I,J)
          ELSE
            K=RANK(I,J)+2
          ENDIF 
          EXPNTERM(I,K)=COLTERM(I,J)
          MORTTERM(I,K)=DEATHTERM(I,J)
          FCANCMX(I,K)=USEFRAC(I,J)
          CHNGFRAC(I,K)=FCANCMX(I,K)-PFCANCMX(I,K)
C
591     CONTINUE
590   CONTINUE
C
C       ---> FROM HERE ON WE GET BACK TO OUR USUAL 9 PFTs <----
C
C     GET BARE FRACTION
C
      DO 600 J = 1, ICC
        DO 601 I = IL1, IL2
          BAREFRAC(I)=BAREFRAC(I)-FCANCMX(I,J)
          PBAREFRA(I)=PBAREFRA(I)-PFCANCMX(I,J)
601     CONTINUE
600   CONTINUE
C
C     CHECK IF A PFT's FRACTIONAL COVER IS INCREASING OR DECREASING
C
      DO 620 J = 1, ICC
        DO 621 I = IL1, IL2
          IF( ( FCANCMX(I,J).GT.PFCANCMX(I,J)) .AND.
     &        (ABS(PFCANCMX(I,J)-FCANCMX(I,J)).GT.ZERO) ) THEN
              FRACIORD(I,J)=1
          ELSE IF( ( FCANCMX(I,J).LT.PFCANCMX(I,J)) .AND.
     &             (ABS(PFCANCMX(I,J)-FCANCMX(I,J)).GT.ZERO) ) THEN
              FRACIORD(I,J)=-1
          ENDIF
621     CONTINUE
620   CONTINUE
C
C     CHECK IF BARE FRACTION INCREASES OF DECREASES
C
      DO 640 I = IL1, IL2
        IF( ( BAREFRAC(I).GT.PBAREFRA(I)) .AND.
     &      (ABS(PBAREFRA(I)-BAREFRAC(I)).GT.ZERO) ) THEN
              BAREIORD(I)=1
        ELSE IF ( ( BAREFRAC(I).LT.PBAREFRA(I)) .AND.
     &            (ABS(PBAREFRA(I)-BAREFRAC(I)).GT.ZERO) ) THEN
              BAREIORD(I)=-1
        ENDIF
640   CONTINUE
C
C     NOW THAT WE KNOW THE CHANGE IN FRACTION FOR EVERY PFT WE USE ITS
C     NPP FOR SPATIAL EXPANSION AND LITTER GENERATION. WE ALSO SPREAD
C     VEGETATION BIOMASS UNIFORMLY OVER THE NEW FRACTIONS, AND GENERATE
C     ADDITIONAL LITTER FROM MORTALITY IF THE FRACTIONS DECREASE.
C
C     THREE THINGS CAN HAPPEN HERE
C
C     1. FRACIORD = 0, WHICH MEANS ALL NPP THAT WAS USED FOR EXPANSION 
C        BECOMES LITTER, DUE TO SELF/EXPANSION THINNING AND MORTALITY.
C
C     2. FRACIORD = 1, WHICH MEANS A PART OF OR FULL NPP IS USED FOR
C        EXPANSION BUT SOME LITTER MAY ALSO BE GENERATED. THE PART OF 
C        NPP THAT IS USED FOR EXPANSION NEEDS TO BE ALLOCATED TO LEAVES,
C        STEM, AND ROOT. RATHER THAN DOING THIS HERE WE WILL LET THE
C        ALLOCATION PART HANDLE THIS. SO ALLOCATION MODULE WILL ALLOCATE
C        NOT ONLY THE NPP THAT IS USED FOR PURE VERTICAL EXPANSION BUT 
C        ALSO THIS NPP. BUT WE WILL DO OUR PART HERE AND SPREAD THE
C        VEGETATION BIOMASS OVER THE NEW INCREASED FRACTION.
C
C     3. FRACIORD = -1, WHICH MEANS ALL OF THE NPP IS TO BE USED FOR
C        LITTER GENERATION BUT IN ADDITION SOME MORE LITTER WILL BE
C        GENERATED FROM MORTALITY OF THE STANDING BIOMASS.
C

      DO 660 J = 1, ICC
       IF(.NOT. CROP(J))THEN  ! DO NOT RUN FOR CROPS
        DO 661 I = IL1, IL2
C
          IF(FRACIORD(I,J).EQ.1)THEN
C
C           REDUCE BIOMASS DENSITY BY SPREADING OVER LARGER FRACTION
C
            TERM = (PFCANCMX(I,J)/FCANCMX(I,J))
            GLEAFMAS(I,J) = GLEAFMAS(I,J)*TERM
            BLEAFMAS(I,J) = BLEAFMAS(I,J)*TERM
            STEMMASS(I,J) = STEMMASS(I,J)*TERM
            ROOTMASS(I,J) = ROOTMASS(I,J)*TERM
            LITRMASS(I,J) = LITRMASS(I,J)*TERM
            SOILCMAS(I,J) = SOILCMAS(I,J)*TERM
C
C           ONLY A FRACTION OF NPP BECOMES LITTER WHICH FOR SIMPLICITY
C           AND FOR NOW WE SPREAD OVER THE WHOLE GRID CELL
C
            IF(EXPNTERM(I,J).LE.ZERO.AND.MORTTERM(I,J).GT.ZERO)THEN
              WRITE(6,*)'EXPANSION TERM<= ZERO WHEN FRACTIONAL COVERAGE'
              WRITE(6,*)'IS INCREASING FOR PFT',J,' IN GRID CELL',I
              write(*,*)'PFCANCMX(',I,',',J,')=',PFCANCMX(I,J)
              write(*,*)'FCANCMX(',I,',',J,')=',FCANCMX(I,J)
              write(*,*)'EXPNTERM(',I,',',J,')=',EXPNTERM(I,J)
              CALL XIT('COMPETITION',-7)
            ELSE IF(EXPNTERM(I,J).LE.ZERO.AND.MORTTERM(I,J).LE.ZERO)THEN
              TERM = 1.0
            ELSE
              TERM = (MORTTERM(I,J)/EXPNTERM(I,J))
            ENDIF
C
            ADD2ALLO(I,J)=(1.-TERM)
C
C           THE FACTOR (DELTAT/963.62) CONVERTS NPP FROM u-MOL CO2-C/M2.SEC 
C           -> Kg C/M2.DELTAT
C
            TERM = TERM*MAX(0.0,NPPVEG(I,J))*(DELTAT/963.62)*LAMBDA(I,J)
     &             *PFCANCMX(I,J)

            INCRLITR(I,J)=TERM
            GRSUMLIT(I)=GRSUMLIT(I)+INCRLITR(I,J)
C
C           REST PUT ASIDE FOR ALLOCATION
C
            ADD2ALLO(I,J) = ADD2ALLO(I,J)* MAX(0.0,NPPVEG(I,J))*
     &                     (DELTAT/963.62)*LAMBDA(I,J)*
     &                     (PFCANCMX(I,J)/FCANCMX(I,J))

C
          ELSE IF(FRACIORD(I,J).EQ.-1)THEN
C
C           ALL NPP USED FOR EXPANSION BECOMES LITTER PLUS THERE IS
C           ADDITIONAL MORTALITY OF THE STANDING BIOMASS. THE NPP THAT 
C           BECOMES LITTER IS NOW SPREAD OVER THE WHOLE GRID CELL.
C           ALL BIOMASS FROM FRACTION THAT DIES DUE TO MORTALITY IS 
C           ALSO DISTRIBUTED OVER THE LITTER POOL OF WHOLE GRID CELL.
C
            TERM = ABS(CHNGFRAC(I,J))*GRCLAREA(I)*1.0E06*(GLEAFMAS(I,J)+
     &          BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J)+LITRMASS(I,J))
            TERM = TERM/(GRCLAREA(I)*1.0E06)

            INCRLITR(I,J)=TERM
C
            TERM = MAX(0.0,NPPVEG(I,J))*(DELTAT/963.62)*LAMBDA(I,J)*
     &              PFCANCMX(I,J)

            INCRLITR(I,J) = INCRLITR(I,J)+TERM
            GRSUMLIT(I)=GRSUMLIT(I)+INCRLITR(I,J)
C
C           CHOP OFF SOIL C FROM THE FRACTION THAT GOES DOWN AND
C           SPREAD IT UNIFORMLY OVER THE SOIL C POOL OF ENTIRE GRID CELL
C
            TERM = ABS(CHNGFRAC(I,J))*GRCLAREA(I)*1.0E06*SOILCMAS(I,J)
            TERM = TERM/(GRCLAREA(I)*1.0E06)
            INCRSOLC(I,J)=TERM
            GRSUMSOC(I)=GRSUMSOC(I)+INCRSOLC(I,J)
C
          ELSE IF(FRACIORD(I,J).EQ.0)THEN
C
C           ALL NPP USED FOR EXPANSION BECOMES LITTER
C
            INCRLITR(I,J) = 
     &                  MAX(0.0,NPPVEG(I,J))*(DELTAT/963.62)*LAMBDA(I,J)
     &                  * PFCANCMX(I,J)
            GRSUMLIT(I)=GRSUMLIT(I)+INCRLITR(I,J)
C
          ENDIF
C
661     CONTINUE
       ENDIF
660   CONTINUE
C
C     IF BARE FRACTION DECREASES THEN CHOP OFF THE LITTER AND SOIL C
C     FROM THE DECREASED FRACTION AND ADD IT TO GRSUMLIT & GRSUMSOC
C     FOR SPREADING OVER THE WHOLE GRID CELL. IF BARE FRACTION INCREASES
C     THEN SPREAD ITS LITTER AND SOIL C UNIFORMLY OVER THE INCREASED 
C     FRACTION.
C
      DO 680 I = IL1, IL2
        IF(BAREIORD(I).EQ.-1)THEN
C
          TERM =(PBAREFRA(I)-BAREFRAC(I))*LITRMASS(I,ICC+1)
          INCRLITR(I,ICC+1)=TERM
          GRSUMLIT(I)=GRSUMLIT(I)+TERM
C
          TERM =(PBAREFRA(I)-BAREFRAC(I))*SOILCMAS(I,ICC+1)
          INCRSOLC(I,ICC+1)=TERM
          GRSUMSOC(I)=GRSUMSOC(I)+TERM
C
        ELSE IF(BAREIORD(I).EQ.1)THEN
C
          TERM = PBAREFRA(I)/BAREFRAC(I)
          LITRMASS(I,ICC+1)=LITRMASS(I,ICC+1)*TERM
          SOILCMAS(I,ICC+1)=SOILCMAS(I,ICC+1)*TERM
C
        ENDIF
680   CONTINUE
C
C     IF A PFT IS NOT SUPPOSE TO EXIST AS INDICATED BY PFTEXIST AND ITS 
C     FRACTIONAL COVERAGE IS REALLY SMALL THEN GET RID OF THE PFT ALL
C     TOGETHER AND SPREAD ITS LIVE AND DEAD BIOMASS OVER THE GRID CELL.
C
      DO 690 J = 1, ICC
        DO 691 I = IL1, IL2
          IF(PFTEXIST(I,J).EQ.0.AND.FCANCMX(I,J).LT.1.0E-05)THEN
C
            TERM = FCANCMX(I,J)*(GLEAFMAS(I,J)+BLEAFMAS(I,J)
     &         +STEMMASS(I,J)+ROOTMASS(I,J)+LITRMASS(I,J))
            INCRLITR(I,J)=INCRLITR(I,J) + TERM
            GRSUMLIT(I)=GRSUMLIT(I)+INCRLITR(I,J)
C
            TERM = FCANCMX(I,J)*SOILCMAS(I,J)
            INCRSOLC(I,J)=INCRSOLC(I,J) + TERM
            GRSUMSOC(I)=GRSUMSOC(I)+INCRSOLC(I,J)
C
            BAREFRAC(I)=BAREFRAC(I)+FCANCMX(I,J)
C
C           ADJUST LITTER AND SOIL C MASS DENSITIES FOR INCREASE IN
C           BAREFRAC OVER THE BARE FRACTION.
C
            TERM = (BAREFRAC(I)-FCANCMX(I,J))/BAREFRAC(I)
            LITRMASS(I,ICC+1) = LITRMASS(I,ICC+1)*TERM
            SOILCMAS(I,ICC+1) = SOILCMAS(I,ICC+1)*TERM
C
            FCANCMX(I,J)=0.0
          ENDIF
691     CONTINUE
690   CONTINUE
C
C     SPREAD LITTER AND SOIL C OVER ALL PFTs AND THE BAREFRAC
C
      DO 700 J = 1, ICC
        DO 701 I = IL1, IL2
          IF(FCANCMX(I,J).GT.ZERO)THEN
            LITRMASS(I,J)=LITRMASS(I,J)+GRSUMLIT(I)
            SOILCMAS(I,J)=SOILCMAS(I,J)+GRSUMSOC(I)
          ELSE
            GLEAFMAS(I,J)=0.0
            BLEAFMAS(I,J)=0.0
            STEMMASS(I,J)=0.0
            ROOTMASS(I,J)=0.0
            LITRMASS(I,J)=0.0
            SOILCMAS(I,J)=0.0
          ENDIF
701     CONTINUE
700   CONTINUE
C
      DO 720 I = IL1, IL2
        IF(BAREFRAC(I).GT.ZERO)THEN
          LITRMASS(I,ICC+1)=LITRMASS(I,ICC+1)+GRSUMLIT(I)
          SOILCMAS(I,ICC+1)=SOILCMAS(I,ICC+1)+GRSUMSOC(I)
        ELSE
          LITRMASS(I,ICC+1)=0.0
          SOILCMAS(I,ICC+1)=0.0
        ENDIF
720   CONTINUE
C
C     GET FCANMXs FOR USE BY CLASS BASED ON THE NEW FCANCMXs
C
      DO 740 J = 1, IC
        DO 741 I = IL1, IL2
           FCANMX(I,J)=0.0 ! FRACTIONAL COVERAGE OF CLASS' PFTs
741     CONTINUE
740   CONTINUE
C
      K1=0
      DO 750 J = 1, IC
        IF(J.EQ.1) THEN
          K1 = K1 + 1
        ELSE
          K1 = K1 + NOL2PFTS(J-1)
        ENDIF
        K2 = K1 + NOL2PFTS(J) - 1
        DO 751 L = K1, K2
          DO 752 I = IL1, IL2
            FCANMX(I,J)=FCANMX(I,J)+FCANCMX(I,L)
752       CONTINUE
751     CONTINUE
750   CONTINUE
C
C     UPDATE GRID AVERAGED VEGETATION BIOMASS, AND LITTER AND SOIL C
C     DENSITIES
C
C
      DO 800 J = 1, ICC
        DO 801 I = IL1, IL2
          VGBIOMAS(I)=VGBIOMAS(I)+FCANCMX(I,J)*(GLEAFMAS(I,J)+
     &                BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J))
          GAVGLTMS(I)=GAVGLTMS(I)+FCANCMX(I,J)*LITRMASS(I,J)
          GAVGSCMS(I)=GAVGSCMS(I)+FCANCMX(I,J)*SOILCMAS(I,J)
801     CONTINUE
800   CONTINUE
C
      DO 810 I = IL1, IL2
        GAVGLTMS(I)=GAVGLTMS(I)+( BAREFRAC(I)*LITRMASS(I,ICC+1) )
        GAVGSCMS(I)=GAVGSCMS(I)+( BAREFRAC(I)*SOILCMAS(I,ICC+1) )
810   CONTINUE
C
C     AND FINALLY WE CHECK THE C BALANCE. WE WERE SUPPOSE TO USE A
C     FRACTION OF NPP FOR COMPETITION. SOME OF IT IS USED FOR EXPANSION
C     (THIS IS WHAT WE SAVE FOR ALLOCATION), AND THE REST BECOMES LITTER. 
C     SO FOR EACH PFT THE TOTAL C MASS IN VEGETATION AND LITTER POOLS
C     MUST ALL ADD UP TO THE SAME VALUE AS BEFORE COMPETITION.
C
      DO 830 J = 1, ICC
       IF (.NOT. CROP(J)) THEN  !FLAG! THIS SHOULDN'T CHECK FOR CROPS RIGHT? JM
        DO 831 I = IL1, IL2

          BIOMASVG(I,J)=FCANCMX(I,J)*GRCLAREA(I)*1.0E06*
     &      (GLEAFMAS(I,J)+BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J)) 
          PBIOMASVG(I,J)=PFCANCMX(I,J)*GRCLAREA(I)*1.0E06*
     &      (PGLFMASS(I,J)+PBLFMASS(I,J)+PROTMASS(I,J)+PSTMMASS(I,J)) 
C
C         PART OF NPP THAT WE WILL USE LATER FOR ALLOCATION
          PUTASIDE(I,J)=ADD2ALLO(I,J)*FCANCMX(I,J)*GRCLAREA(I)*1.0E06
C
          GAVGPUTA(I) = GAVGPUTA(I) + PUTASIDE(I,J)
C
C         LITTER ADDED TO BARE
          BARELITR(I,J)=GRSUMLIT(I)*GRCLAREA(I)*1.0E06*FCANCMX(I,J)
          OWNLITR(I,J)=INCRLITR(I,J)*GRCLAREA(I)*1.0E06
C
C         SOIL C ADDED TO BARE
          BARESOLC(I,J)=GRSUMSOC(I)*GRCLAREA(I)*1.0E06*FCANCMX(I,J)
          OWNSOLC(I,J)=INCRSOLC(I,J)*GRCLAREA(I)*1.0E06
C
          ADD2DEAD(I) = ADD2DEAD(I) + BARELITR(I,J) + BARESOLC(I,J)
C
C         NPP WE HAD IN FIRST PLACE TO EXPAND
          NPPVEGAR(I,J)=MAX(0.0,NPPVEG(I,J))*(DELTAT/963.62)*
     &      LAMBDA(I,J)*GRCLAREA(I)*1.0E06*PFCANCMX(I,J) 
C
          GAVGNPP(I) = GAVGNPP(I) + NPPVEGAR(I,J)
C
          DEADMASS(I,J)=FCANCMX(I,J)*GRCLAREA(I)*1.0E06*
     &      (LITRMASS(I,J)+SOILCMAS(I,J))
          PDEADMAS(I,J)=PFCANCMX(I,J)*GRCLAREA(I)*1.0E06*
     &      (PLTRMASS(I,J)+PSOCMASS(I,J))
C
C         TOTAL MASS BEFORE COMPETITION
          BEFRMASS=PBIOMASVG(I,J)+NPPVEGAR(I,J)+PDEADMAS(I,J)
C
C         TOTAL MASS AFTER COMPETITION
          AFTRMASS=BIOMASVG(I,J)+PUTASIDE(I,J)+DEADMASS(I,J)-
     &             BARELITR(I,J)-BARESOLC(I,J)+OWNLITR(I,J)+
     &             OWNSOLC(I,J)

          IF(ABS(BEFRMASS-AFTRMASS).GT.TOLRANC1)THEN
            WRITE(6,*)'TOTAL BIOMASS FOR PFT',J,', AND GRID CELL =',I 
            WRITE(6,*)'DOES NOT BALANCE BEFORE AND AFTER COMPETITION'
            WRITE(6,*)' '
            WRITE(6,*)'CHNGFRAC(',I,',',J,')=',CHNGFRAC(I,J)
            WRITE(6,*)'FRACIORD(',I,',',J,')=',FRACIORD(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'PBIOMASVG(',I,',',J,')=',PBIOMASVG(I,J)
            WRITE(6,*)'PDEADMAS(',I,',',J,')=',PDEADMAS(I,J)
            WRITE(6,*)'NPPVEGAR(',I,',',J,')=',NPPVEGAR(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'BIOMASVG(',I,',',J,')=',BIOMASVG(I,J)
            WRITE(6,*)'DEADMASS(',I,',',J,')=',DEADMASS(I,J)
            WRITE(6,*)'PUTASIDE(',I,',',J,')=',PUTASIDE(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'BEFORE BIOMASS DENSITY = ',GLEAFMAS(I,J)+
     &             BLEAFMAS(I,J)+STEMMASS(I,J)+ROOTMASS(I,J)
            WRITE(6,*)'AFTER  BIOMASS DENSITY = ',PGLFMASS(I,J)+
     &            PBLFMASS(I,J)+PROTMASS(I,J)+PSTMMASS(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'BARELITR(',I,',',J,')=',BARELITR(I,J)
            WRITE(6,*)'BARESOLC(',I,',',J,')=',BARESOLC(I,J)
            WRITE(6,*)'OWNLITR(',I,',',J,')=',OWNLITR(I,J)
            WRITE(6,*)'OWNSOLC(',I,',',J,')=',OWNSOLC(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'GRCLAREA(',I,')=',GRCLAREA(I)
            WRITE(6,*)'FCANCMX(',I,',',J,')=',FCANCMX(I,J)
            WRITE(6,*)'PFCANCMX(',I,',',J,')=',PFCANCMX(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'ABS(BEFRMASS-AFTRMASS)=',ABS(BEFRMASS-AFTRMASS)
            CALL XIT('COMPETITION',-8)
          ENDIF
C
831     CONTINUE
       ENDIF
830   CONTINUE
C
C     CHECK BALANCE OVER THE BARE FRACTION
C
      DO 850 J = ICC+1, ICC+1
        DO 851 I = IL1, IL2
          DEADMASS(I,J)=BAREFRAC(I)*GRCLAREA(I)*1.0E06*
     &      (LITRMASS(I,J)+SOILCMAS(I,J))
          PDEADMAS(I,J)=PBAREFRA(I)*GRCLAREA(I)*1.0E06*
     &      (PLTRMASS(I,J)+PSOCMASS(I,J))
C
          ADD2DEAD(I)=(GRSUMLIT(I)+GRSUMSOC(I))*BAREFRAC(I)*
     &      GRCLAREA(I)*1.0E06
C
          OWNLITR(I,J)=INCRLITR(I,J)*GRCLAREA(I)*1.0E06
          OWNSOLC(I,J)=INCRSOLC(I,J)*GRCLAREA(I)*1.0E06

          BEFRMASS=PDEADMAS(I,J)+ADD2DEAD(I)
          AFTRMASS=DEADMASS(I,J)+OWNLITR(I,J)+OWNSOLC(I,J)
C
          IF(ABS(BEFRMASS-AFTRMASS).GT.TOLRANC1)THEN
            WRITE(6,*)'TOTAL DEAD MASS FOR GRID CELL =',I,
     &                'DOES NOT BALANCE OVER BARE' 
            WRITE(6,*)'PDEADMAS(',I,',',J,')=',PDEADMAS(I,J)
            WRITE(6,*)'ADD2DEAD(',I,') TERM=',ADD2DEAD(I)
            WRITE(6,*)'DEADMASS(',I,',',J,')=',DEADMASS(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'OWNLITR(',I,',',J,')=',OWNLITR(I,J)
            WRITE(6,*)'OWNSOLC(',I,',',J,')=',OWNSOLC(I,J)
            WRITE(6,*)' '
            WRITE(6,*)'PBAREFRA(',I,')=',PBAREFRA(I)
            WRITE(6,*)'BAREIORD(',I,') =',BAREIORD(I)
            WRITE(6,*)'BAREFRAC(',I,')=',BAREFRAC(I)
            WRITE(6,*)' '
            WRITE(6,*)'ABS(BEFRMASS-AFTRMASS)=',ABS(BEFRMASS-AFTRMASS)
            CALL XIT('COMPETITION',-9)
          ENDIF
851     CONTINUE
850   CONTINUE
C
C     GRID AVERAGED DENSITIES MUST ALSO BALANCE
C
      DO 870 I = IL1, IL2
        BEFRMASS=(PVGBIOMS(I)+PGAVLTMS(I)+PGAVSCMS(I))
     &           +GAVGNPP(I)/(GRCLAREA(I)*1.0E06)
        AFTRMASS=(VGBIOMAS(I)+GAVGLTMS(I)+GAVGSCMS(I))
     &           +GAVGPUTA(I)/(GRCLAREA(I)*1.0E06)
        IF(ABS(BEFRMASS-AFTRMASS).GT.TOLRANC2)THEN
          WRITE(6,*)'TOTAL (LIVE+DEAD) MASS FOR GRID CELL =',I,
     &              'DOES NOT BALANCE' 
          WRITE(6,*)'ABS(BEFRMASS-AFTRMASS)',ABS(BEFRMASS-AFTRMASS),'IS
     &GT OUR TOLERANCE OF',TOLRANC2
          WRITE(6,*)'PVGBIOMS(',I,')=',PVGBIOMS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'PGAVLTMS(',I,')=',PGAVLTMS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'PGAVSCMS(',I,')=',PGAVSCMS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'GAVGNPP(',I,')=',GAVGNPP(I)
          WRITE(6,*)'BEFRMASS*1.0E06 =',BEFRMASS*1.0E06
          WRITE(6,*)' '
          WRITE(6,*)'VGBIOMAS(',I,')=',VGBIOMAS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'GAVGLTMS(',I,')=',GAVGLTMS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'GAVGSCMS(',I,')=',GAVGSCMS(I)*GRCLAREA(I)*1.0E06
          WRITE(6,*)'GAVGPUTA(',I,')=',GAVGPUTA(I)
          WRITE(6,*)'AFTRMASS*1.0E06=',AFTRMASS*1.0E06
          CALL XIT('COMPETITION',-10)
        ENDIF
870   CONTINUE
C
      RETURN
      END
