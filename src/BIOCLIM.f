      SUBROUTINE  BIOCLIM (   IDAY,        TA,   PRECIP,   NETRAD,
     1                         IL1,       IL2,      ILG,
C    ------------------- INPUTS ABOVE THIS LINE ------------------
     2                       TCURM,  SRPCURYR, DFTCURYR,  INIBOCLM,
     3                      TMONTH,  ANPCPCUR,  ANPECUR,   GDD5CUR,
     4                    SURMNCUR,  DEFMNCUR, SRPLSCUR,  DEFCTCUR,
C    -------------------UPDATES ABOVE THIS LINE ------------------
     5                      TWARMM,    TCOLDM,     GDD5,  ARIDITY,
     6                    SRPLSMON,  DEFCTMON, ANNDEFCT, ANNSRPLS,
     7                      ANNPCP,  ANPOTEVP)    
C    -------------------OUTPUTS ABOVE THIS LINE ------------------
C
C               CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
C                BIOCLIMATIC PARAMETERS ESTIMATION SUBROUTINE 
C
C     25  MAY 2004  - THIS SUBROUTINE CALCULATES THE BIOCLIMATIC
C     V. ARORA        PARAMETERS THAT ARE REQUIRED FOR DETERMINING
C                     EXISTENCE OF PFTs. THE BIOCLIMATIC PARAMETERS 
C                     ARE THE MEAN MONTHLY TEMPERATURE OF THE WARMEST
C                     AND THE COLDEST MONTHS, GROWING DEGREE DAYS 
C                     ABOVE 5 C, ANNUAL PRECIPITATION AND POTENTIAL
C                     EVAPORATION AND SOME ARIDITY PARAMETERS THAT ARE
C                     FUNCTION OF POTENTIAL EVAPORATION AND PRECIPITATION.
C
C                     IN ADDITION, ALL THESE PARAMETERS ARE UPDATED
C                     IN AN E-FOLDING SENSE AT SOME SPECIFIED TIME
C                     SCALE.
C     INPUTS 
C
C     IDAY      - DAY OF THE YEAR
C     TA        - MEAN DAILY TEMPERATURE, K
C     PRECIP    - DAILY PRECIPITATION (MM/DAY)
C     NETRAD    - DAILY NET RADIATION (W/M2)
C     ILG       - NO. OF GRID CELLS IN LATITUDE CIRCLE
C     IL1,IL2   - IL1=1, IL2=ILG
C
C     UPDATES
C
C     TCURM     - TEMPERATURE OF THE CURRENT MONTH (C)
C     SRPCURYR  - WATER SURPLUS FOR THE CURRENT YEAR
C     DFTCURYR  - WATER DEFICIT FOR THE CURRENT YEAR
C     INIBOCLM  - SWITCH TELLING IF BIOCLIMATIC PARAMETERS ARE BEING
C                 INITIALIZED FROM SCRATCH (FALSE) OR BEING INITIALIZED
C                 FROM SOME SPUN UP VALUES(TRUE).
C     TMONTH    - MONTHLY TEMPERATURES
C     ANPCPCUR  - ANNUAL PRECIPITATION FOR CURRENT YEAR (MM)
C     ANPECUR   - ANNUAL POTENTIAL EVAPORATION FOR CURRENT YEAR (MM)
C     GDD5CUR   - GROWING DEGREE DAYS ABOVE 5 C FOR CURRENT YEAR
C     SURMNCUR  - NUMBER OF MONTHS WITH SURPLUS WATER FOR CURRENT YEAR
C     DEFMNCUR  - NUMBER OF MONTHS WITH WATER DEFICIT FOR CURRENT YEAR
C     SRPLSCUR  - WATER SURPLUS FOR THE CURRENT MONTH
C     DEFCTCUR  - WATER DEFICIT FOR THE CURRENT MONTH
C
C     OUTPUTS
C     FOLLOWING ARE RUNNING AVERAGES IN AN E-FOLDING SENSE
C
C     TWARMM    - TEMPERATURE OF THE WARMEST MONTH (C)
C     TCOLDM    - TEMPERATURE OF THE COLDEST MONTH (C)
C     GDD5      - GROWING DEGREE DAYS ABOVE 5 C
C     ARIDITY   - ARIDITY INDEX, RATIO OF POTENTIAL EVAPORATION TO
C                 PRECIPITATION
C     SRPLSMON  - NUMBER OF MONTHS IN A YEAR WITH SURPLUS WATER I.E.
C                 PRECIPITATION MORE THAN POTENTIAL EVAPORATION
C     DEFCTMON  - NUMBER OF MONTHS IN A YEAR WITH WATER DEFICIT I.E.
C                 PRECIPITATION LESS THAN POTENTIAL EVAPORATION
C     ANNDEFCT  - ANNUAL WATER DEFICIT (MM) 
C     ANNSRPLS  - ANNUAL WATER SURPLUS (MM)
C     ANNPCP    - ANNUAL PRECIPITATION (MM)
C     ANPOTEVP  - ANNUAL POTENTIAL EVAPORATION (MM)
C 
      IMPLICIT NONE
C
      INTEGER IDAY, IL1, IL2, ILG
C
      LOGICAL INIBOCLM
C
      REAL  TWARMM(ILG),   TCOLDM(ILG),      GDD5(ILG),    ARIDITY(ILG),
     1      PRECIP(ILG),       TA(ILG),    NETRAD(ILG),      TCURM(ILG),
     2      ANNPCP(ILG), ANPOTEVP(ILG), TMONTH(12,ILG),    TCCURYR(ILG),
     3     TWCURYR(ILG),  GDD5CUR(ILG),  ANPCPCUR(ILG),    ANPECUR(ILG),
     4     ARIDCUR(ILG), SRPLSCUR(ILG),  DEFCTCUR(ILG),          WTRBAL,
     5    SRPLSMON(ILG), DEFCTMON(ILG),  SRPCURYR(ILG),   DFTCURYR(ILG),
     6    ANNDEFCT(ILG), ANNSRPLS(ILG)
C
      REAL  EFTIME, FACTOR, ZERO
C
      INTEGER MONTHDAYS(12), MONTHEND(13), MONTH, ATMONTHEND, I, J,
     1                    K,     CURMONTH,     SURMNCUR(ILG),  
     2        DEFMNCUR(ILG)
C
C     ------------------------------------------------------------------
C                           PARAMETERS USED 
C
C     E-FOLDING TIME SCALE FOR UPDATING BIOCLIMATIC PARAMETERS (YEARS)
      DATA  EFTIME/25.00/
C
C     DAYS IN EACH MONTH
      DATA  MONTHDAYS/31,28,31,30,31,30,31,31,30,31,30,31/
C
C     CALENDER DAY AT END OF EACH MONTH
      DATA MONTHEND/0,31,59,90,120,151,181,212,243,273,304,334,365/
C
C     ZERO
      DATA ZERO/1.0E-20/
C     ---------------------------------------------------------------
C
C     INITIALIZATIONS
C
      IF(IDAY.EQ.1)THEN
        DO 100 I = IL1, IL2
          GDD5CUR(I)=0.0    ! GDD5 FOR THE CURRENT YEAR
          ANPCPCUR(I)=0.0   ! ANNUAL PRECIP. FOR THE CURRENT YEAR
          ANPECUR(I)=0.0    ! ANNUAL POTENTIAL EVAP FOR THE CURRENT YEAR
          ARIDCUR(I)=100.0  ! ARIDITY INDEX FOR THE CURRENT YEAR
          SURMNCUR(I)=0     ! MONTHS WITH SURPLUS WATER FOR CURRENT YEAR
          DEFMNCUR(I)=0     ! MONTHS WITH WATER DEFICIT FOR CURRENT YEAR
          SRPCURYR(I)=0.0   ! CURRENT YEAR'S WATER SURPLUS
          DFTCURYR(I)=0.0   ! CURRENT YEAR'S WATER DEFICIT
100     CONTINUE      
      ENDIF
C
C     FIND IF WE ARE AT END OF MONTH OR NOT
C
      ATMONTHEND=0
      DO 150 MONTH = 1, 12
        IF(IDAY.EQ.MONTHEND(MONTH+1)) THEN
          ATMONTHEND=1
          GOTO 160
        ENDIF
150   CONTINUE
160   CONTINUE
C
C     IF AT A MONTH'S BEGINNING THEN INITIALIZE TEMPERATURE OF CURRENT
C     MONTH AND CURRENT MONTHS WATER SURPLUS/DEFICIT TO ZERO
C
      DO 200 MONTH = 1, 12
        DO 210 I = IL1, IL2
          IF(IDAY.EQ.(MONTHEND(MONTH)+1))THEN
            TCURM(I)=0.0    ! TEMPERATURE OF CURRENT MONTH
            SRPLSCUR(I)=0.0 ! CURRENT MONTH'S WATER SURPLUS
            DEFCTCUR(I)=0.0 ! CURRENT MONTH'S WATER DEFICIT
          ENDIF
210     CONTINUE
200   CONTINUE
C
C     FIND CURRENT MONTH
C
      CURMONTH=0
      DO 220 K = 2, 13
        IF(IDAY.GE.(MONTHEND(K-1)+1).AND.IDAY.LE.MONTHEND(K))THEN
          CURMONTH=K-1
        ENDIF
220   CONTINUE
C
      IF(CURMONTH.EQ.0)THEN
        CALL XIT('BIOCLIM',-1)
      ENDIF
C
C     UPDATE MONTHLY TEMPERATURE FOR THE CURRENT MONTH, AND OTHER
C     VARIABLES. AT THE END OF THE MONTH WE WILL HAVE AVERAGE OF 
C     ALL DAILY TEMPERATURES FOR THE CURRENT MONTH.
C
      DO 240 I = IL1, IL2
          TCURM(I)=TCURM(I)+(TA(I)-273.16)*
     &             (1.0/REAL(MONTHDAYS(CURMONTH)))
          GDD5CUR(I)=GDD5CUR(I)+MAX(0.0, (TA(I)-273.16-5.0))
          ANPCPCUR(I)=ANPCPCUR(I) + PRECIP(I)
C         NET RADIATION (W/M2) x 12.87 = POTENTIAL EVAP (MM)
          ANPECUR(I)=ANPECUR(I) + NETRAD(I)*12.87*(1.0/365.0)
          WTRBAL=PRECIP(I)-(NETRAD(I)*12.87*(1.0/365.0))
          IF(WTRBAL.GE.0.0)THEN
            SRPLSCUR(I)=SRPLSCUR(I)+WTRBAL
          ELSE IF(WTRBAL.LT.0.0)THEN
            DEFCTCUR(I)=DEFCTCUR(I)+ABS(WTRBAL)
          ENDIF
240   CONTINUE
C
C     IF ITS THE END OF THE MONTH THEN STORE THE MONTHLY TEMPERATURE 
C     AND SET TCURM EQUAL TO ZERO. ALSO CHECK IF THIS MONTH HAD WATER
C     DEFICIT OR SURPLUS
C
      DO 250 I = IL1, IL2
        IF(ATMONTHEND.EQ.1)THEN
          TMONTH(CURMONTH,I)=TCURM(I)
          TCURM(I)=0.0
          IF( SRPLSCUR(I).GE.DEFCTCUR(I) )THEN
            SURMNCUR(I) = SURMNCUR(I) + 1
          ELSE IF(SRPLSCUR(I).LT.DEFCTCUR(I) )THEN
            DEFMNCUR(I) = DEFMNCUR(I) + 1
          ENDIF
          SRPCURYR(I)=SRPCURYR(I)+SRPLSCUR(I)
          DFTCURYR(I)=DFTCURYR(I)+DEFCTCUR(I)
        ENDIF
        IF(IDAY.EQ.365)THEN
          TWCURYR(I)=-9000.0
          TCCURYR(I)=9000.0
          IF(ANPCPCUR(I).GT.ZERO)THEN
            ARIDCUR(I)=ANPECUR(I)/ANPCPCUR(I)
          ELSE
            ARIDCUR(I)=100.0
          ENDIF
        ENDIF
250   CONTINUE
C
C     IF ITS THE END OF YEAR, THEN FIND THE TEMPERATURE OF THE WARMEST
C     AND THE COLDEST MONTH
C
      IF(IDAY.EQ.365)THEN
        DO 260 MONTH = 1, 12
          DO 270 I = IL1, IL2
            IF(TMONTH(MONTH,I).GT.TWCURYR(I))THEN
              TWCURYR(I)=TMONTH(MONTH,I)
            ENDIF 
            IF(TMONTH(MONTH,I).LT.TCCURYR(I))THEN
              TCCURYR(I)=TMONTH(MONTH,I)
            ENDIF 
270       CONTINUE
260     CONTINUE
C       UPDATE LONG TERM MOVING AVERAGE OF BIOCLIMATIC PARAMETERS IN AN 
C       E-FOLDING SENSE
        DO 280 I = IL1, IL2
          FACTOR=EXP(-1.0/EFTIME)
          IF(.NOT. INIBOCLM)THEN
            TWARMM(I)=TWCURYR(I)
            TCOLDM(I)=TCCURYR(I)
            GDD5(I)=GDD5CUR(I)
            ARIDITY(I)=ARIDCUR(I)
            SRPLSMON(I)=REAL(SURMNCUR(I))
            DEFCTMON(I)=REAL(DEFMNCUR(I))
            ANNSRPLS(I)=SRPCURYR(I)
            ANNDEFCT(I)=DFTCURYR(I)
            ANNPCP(I)=ANPCPCUR(I)
            ANPOTEVP(I)=ANPECUR(I)
            INIBOCLM=.TRUE.
          ELSE
            TWARMM(I)=TWARMM(I)*FACTOR + TWCURYR(I)*(1.0-FACTOR)
            TCOLDM(I)=TCOLDM(I)*FACTOR + TCCURYR(I)*(1.0-FACTOR)
            GDD5(I)  =GDD5(I)*FACTOR + GDD5CUR(I)*(1.0-FACTOR)
            ARIDITY(I)=ARIDITY(I)*FACTOR + ARIDCUR(I)*(1.0-FACTOR)
            SRPLSMON(I)=SRPLSMON(I)*FACTOR + REAL(SURMNCUR(I))
     &                  *(1.0-FACTOR)
            DEFCTMON(I)=DEFCTMON(I)*FACTOR + REAL(DEFMNCUR(I))
     &                  *(1.0-FACTOR)
            ANNSRPLS(I)=ANNSRPLS(I)*FACTOR + SRPCURYR(I)*(1.0-FACTOR)
            ANNDEFCT(I)=ANNDEFCT(I)*FACTOR + DFTCURYR(I)*(1.0-FACTOR)
            ANNPCP(I)=ANNPCP(I)*FACTOR + ANPCPCUR(I)*(1.0-FACTOR)
            ANPOTEVP(I)=ANPOTEVP(I)*FACTOR + ANPECUR(I)*(1.0-FACTOR)
          ENDIF
280     CONTINUE
      ENDIF
C
      RETURN
      END

