      SUBROUTINE  EXISTENCE(  IDAY,       IL1,      IL2,      ILG,
     1                         ICC,     SORT,  NOL2PFTS,       IC,
     2                      TWARMM,    TCOLDM,     GDD5,  ARIDITY,
     3                    SRPLSMON,  DEFCTMON, ANNDEFCT, ANNSRPLS,
     4                      ANNPCP,  ANPOTEVP,      
C    ------------------- INPUTS ABOVE THIS LINE ------------------
     5                    PFTEXIST) 
C    -------------------OUTPUTS ABOVE THIS LINE ------------------
C
C               CANADIAN TERRESTRIAL ECOSYSTEM MODEL (CTEM) V1.1
C                          PFT EXISTENCE SUBROUTINE 
C
C     27  MAY 2004  - THIS SUBROUTINE CALCULATES THE EXISTENCE OF
C     V. ARORA        PFTs IN GRID CELLS BASED ON A SET OF BIOCLIMATIC
C                     PARAMETERS THAT ARE ESTIMATED IN A RUNNING AVERAGE
C                     SENSE USING SOME SPECIFIED TIMESCALE. 
C
C                     IF LONG TERM AVERAGED BIOCLIMATIC PARAMETERS
C                     INDICATE NON-EXISTENCE OF A PFT THEN AN ADDITIONAL
C                     MORTALITY TERM KICKS IN THE COMPETITION EQNS. ALSO
C                     WHILE A PFT MAY BE ABLE TO EXIST IN A GRID CELL IT
C                     MAY BE EXCLUDED BY COMPETITION FROM OTHER PFTs.
C
C                -->  NOTE THAT SINCE THE FRACTIONAL COVERAGE OF C3 AND
C                -->  C4 CROPS IS GOING TO BE PRESCRIBED, THE MODEL
C                -->  ASSUMES THAT THESE PFTs CAN ALWAYS EXIST. BUT, OF
C                     COURSE THE PRESCRIBED FRACTIONAL COVERAGE OF THESE 
C                     PFTs WILL DECIDE IF THEY ARE PRESENT IN A GRID CELL OR
C                     NOT.
C
C     INPUTS 
C
C     IDAY      - DAY OF THE YEAR
C     ILG       - NO. OF GRID CELLS IN LATITUDE CIRCLE
C     IL1,IL2   - IL1=1, IL2=ILG
C     ICC       - NUMBER OF CTEM PFTs
C     SORT      - INDEX FOR CORRESPONDENCE BETWEEN 9 CTEM PFTs AND
C                 SIZE 12 OF PARAMETER VECTORS
C     NOL2PFTS  - NUMBER OF LEVEL 2 CTEM PFTs
C     IC        - NUMBER OF CLASS PFTs
C     TWARMM    - TEMPERATURE OF THE WARMEST MONTH (C)
C     TCOLDM    - TEMPERATURE OF THE COLDEST MONTH (C)
C     GDD5      - GROWING DEGREE DAYS ABOVE 5 C
C     ARIDITY   - ARIDITY INDEX, RATIO OF POTENTIAL EVAPORATION TO
C                 PRECIPITATION
C     SRPLSMON  - NUMBER OF MONTHS IN A YEAR WITH SURPLUS WATER I.E.
C                 PRECIPITATION MORE THAN POTENTIAL EVAPORATION
C     DEFCTMON  - NUMBER OF MONTHS IN A YEAR WITH WATER DEFICIT I.E.
C                 PRECIPITATION LESS THAN POTENTIAL EVAPORATION
C     ANNDEFCT  - ANNUAL WATER DEFICIT (MM) 
C     ANNSRPLS  - ANNUAL WATER SURPLUS (MM)
C     ANNPCP    - ANNUAL PRECIPITATION (MM)
C     ANPOTEVP  - ANNUAL POTENTIAL EVAPORATION (MM)
C
C     OUTPUTS
C
C     PFTEXIST  - BINARY ARRAY INDICATING PFTs EXIST (=1) OR NOT (=0)
C 
      IMPLICIT NONE
C
      INTEGER I, J, IDAY, IL1, IL2, ILG, ICC, IC, KK, PFTEXIST(ILG,ICC)
C
      PARAMETER (KK=12)  ! PRODUCT OF CLASS PFTs AND L2MAX
C
      INTEGER       SORT(ICC),      NOL2PFTS(IC)
C
      REAL  TWARMM(ILG),   TCOLDM(ILG),      GDD5(ILG),    ARIDITY(ILG),
     2      ANNPCP(ILG), ANPOTEVP(ILG),  SRPLSMON(ILG),   DEFCTMON(ILG),
     3    ANNDEFCT(ILG), ANNSRPLS(ILG)
C
      REAL TCOLDMIN(KK),  TCOLDMAX(KK),    GDD5LMT(KK),     ARIDLMT(KK),
     1     TWARMMAX(KK)
      REAL  ZERO
C
C     ------------------------------------------------------------------
C                           PARAMETERS USED 
C     
C     THE MODEL BASICALLY USES THE TEMPERATURE OF THE COLDEST MONTH AS
C     THE MAJOR CONSTRAINT FOR PFT DISTRIBUTION. A RANGE OF THE COLDEST
C     MONTH TEMPERATURE IS PRESCRIBED FOR EACH PFT WITHIN WHICH PFTS ARE
C     ALLOWED TO EXIST. IN ADDITION FOR TROPICAL BROADLEAF DROUGHT 
C     DECIDUOUS TREES MEASURE(S) OF ARIDITY (FUNCTION OF PRECIPITATION
C     AND POTENTIAL EVAPORATION) ARE USED.
C
C     NOTE THE STRUCTURE OF PARAMETER VECTORS WHICH CLEARLY SHOWS THE 
C     CLASS PFTs (ALONG ROWS) AND CTEM SUB-PFTs (ALONG COLUMNS)
C
C     NEEDLE LEAF |  EVG       DCD       ---
C     BROAD LEAF  |  EVG   DCD-CLD   DCD-DRY
C     CROPS       |   C3        C4       ---
C     GRASSES     |   C3        C4       ---
C
C     NUMBERS SUCH AS 999.9, 99.9 AND 9.9 ARE MEANT TO IMPLY NO CONSTRAIN AND
C     NOT USED 
C
C     MINIMUM COLDEST MONTH TEMPERATURE
      DATA TCOLDMIN/-32.5, -999.9,   0.0,
     &               15.5, -999.9,  15.5,        
     &             -999.9, -999.9,   0.0,
     &             -999.9,   15.5,   0.0/
C
C     MAXIMUM COLDEST MONTH TEMPERATURE
      DATA TCOLDMAX/ 22.0,   -2.0,   0.0,
     &              999.9,   15.5, 900.0,        
     &              999.9,  999.9,   0.0,
     &               15.0,  999.9,   0.0/
C
C     MAXIMUM WARMEST MONTH TEMPERATURE
      DATA TWARMMAX/ 99.9,  23.0,  0.0,
     &               99.9,  99.9, 99.9,        
     &               99.9,  99.9,  0.0,
     &               99.9,  99.9,  0.0/
C
C     MINIMUM GDD ABOVE 5 C REQUIRED TO EXIST
      DATA GDD5LMT/ 600.0,  350.0,    0.0,
     &             1200.0,  350.0,    9.9,        
     &                9.9,    9.9,    0.0,
     &                9.9,    9.9,    0.0/
C
C     ARIDITY INDEX LIMIT FOR BROADLEAF DROUGHT/DRY DECIDUOUS TREES
      DATA ARIDLMT/ 9.9,  9.9,    0.0,
     &              9.9,  9.9,    1.5,        
     &              9.9,  9.9,    0.0,
     &              9.9,  9.9,    0.0/
C
C     ZERO
      DATA ZERO/1.0E-20/
C     ---------------------------------------------------------------
C
C     GO THROUGH ALL GRID CELLS AND BASED ON BIOCLIMATIC PARAMETERS
C     DECIDE IF A GIVEN PFT SHOULD EXIST OR NOT. 
C
      DO 100 I = IL1, IL2
C
C       NEEDLELEAF EVERGREEN
        J=1
        IF(TCOLDM(I).GE.TCOLDMIN(SORT(J)).AND.
     &     TCOLDM(I).LE.TCOLDMAX(SORT(J)).AND.
     &     GDD5(I).GE.GDD5LMT(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       NEEDLELEAF DECIDUOUS
        J=2
        IF(TCOLDM(I).LE.TCOLDMAX(SORT(J)).AND.
     &     TWARMM(I).LE.TWARMMAX(SORT(J)).AND.
     &     GDD5(I).GE.GDD5LMT(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       BROADLEAF EVERGREEN
        J=3
        IF(TCOLDM(I).GE.TCOLDMIN(SORT(J)).AND.
     &     GDD5(I).GE.GDD5LMT(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       BROADLEAF DECIDUOUS COLD
        J=4
        IF(TCOLDM(I).LE.TCOLDMAX(SORT(J)).AND.
     &     GDD5(I).GE.GDD5LMT(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       BROADLEAF DECIDUOUS DRY
        J=5
        IF(TCOLDM(I).GE.TCOLDMIN(SORT(J)).AND.
     &     ARIDITY(I).GE.ARIDLMT(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       C3 AND C4 CROPS
        PFTEXIST(I,6)=1
        PFTEXIST(I,7)=1
C
C       C3 GRASS
        J=8
        IF(TCOLDM(I).LE.TCOLDMAX(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
C       C4 GRASS
        J=9
        IF(TCOLDM(I).GE.TCOLDMIN(SORT(J)))THEN
           PFTEXIST(I,J)=1
        ELSE
           PFTEXIST(I,J)=0
        ENDIF
C
100   CONTINUE
C
      RETURN
      END
